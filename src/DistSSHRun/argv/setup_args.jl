function show_requirements(; io::IO = stdout)
    print_help_chrome("DistSSHRun setup"; io = io)
    print_help_lines(
        io,
        "Deploy and check the project on SSH hosts before go / drive.",
        "Recommended: --rsync, --instantiate, --check, then optional --runtest.",
        "Hosts: child:NAME[:N] (same as go / drive / size; :N ignored).",
        "       parent is `up`, not setup.",
        "Remote path: ~/Parent/RepoName, or --remote-path / ENV.",
        "Git parity is drive --require-git (off by default).",
    )
    print_help_blank(io)
    print_help_section("Usage"; io = io)
    print_help_lines(
        io,
        "  julia --project=. -m DistSSHKit setup MODE child:host1 child:host2",
        "  setup --rsync child:host1 child:host2",
        "  setup --instantiate child:host1 child:host2",
        "  setup --check child:host1 child:host2",
        "  setup --runtest child:host1 child:host2",
    )
    print_help_blank(io)
    print_help_section("Modes (one per run)"; io = io)
    print_help_lines(
        io,
        "  --rsync / --clone    empty remote path only",
        "                       --delete first to replace",
        "  --sync / --pull      git update (confirm unless -y)",
        "  --instantiate        Pkg.instantiate on remotes",
        "  --check              SSH, Julia, project, deps",
        "  --runtest            Pkg.test of the job project on remotes",
        "  --cleanup / --delete stale workers / remote tree",
        "  --prune              .distsshkit go/drive/setup/runs leaves",
    )
    print_help_blank(io)
    print_help_section("Options"; io = io)
    print_help_lines(
        io,
        "  --repo URL           clone URL (default: origin)",
        "  --remote-path PATH   remote project root",
        "  --julia PATH         remote Julia",
        "  $(KIT_QUIET_FLAG_HELP)",
        "  $(KIT_PROGRESS_FLAG_HELP)",
        "  $(KIT_VERBOSE_FLAG_HELP)",
        "  $(KIT_TIME_HELP)",
        "  -y, --yes            skip confirmations",
        "  --hosts CSV          child:NAME[:N] (`:N` stripped)",
        "  --hosts-file PATH    one token per line (`:N` stripped)",
        "  --version, -v        print version and exit",
        "  --older-than DAYS    with --prune: mtime at least DAYS old",
        "  --id TOKEN           with --prune: go / runs leaf name contains TOKEN",
    )
    print_help_blank(io)
    print_help_section("Environment"; io = io)
    print_help_lines(
        io,
        "  $(KIT_SKIP_PKILL_ENV_HELP)",
        "  $(KIT_JOBS_ENV_HELP)",
    )
    print_help_blank(io)
    return print_help_lines(
        io,
        "Details: docs (manual/setup). See also: go, drive, size.",
    )
end

function parse_setup_args(args::Vector{String})
    cli_session, args = peel_kit_cli_flags(args)
    mode = nothing
    julia_path = get(ENV, "JULIA_DISTRIBUTED_EXE", "auto")  # default to env or auto-detect
    repo_url = nothing
    remote_path_override = nothing
    hosts = String[]
    show_help = false
    ignore_julia_version = false
    older_days = nothing
    prune_id = nothing

    c = CliCursor(args)
    while !cli_at_end(c)
        arg = cli_current(c)::String
        if arg == "--check"
            mode = :check
            cli_consume!(c)
        elseif arg == "--pull"
            mode = :pull
            cli_consume!(c)
        elseif arg == "--sync"
            mode = :sync
            cli_consume!(c)
        elseif arg == "--instantiate"
            mode = :instantiate
            cli_consume!(c)
        elseif arg == "--juliaup"
            throw(ArgumentError("setup --juliaup is now: julia -m DistSSHKit up"))
        elseif arg == "--juliaup-update"
            throw(ArgumentError("setup --juliaup-update is now: julia -m DistSSHKit up update"))
        elseif arg == "--runtest"
            mode = :runtest
            cli_consume!(c)
        elseif arg == "--cleanup"
            mode = :cleanup
            cli_consume!(c)
        elseif arg == "--clone"
            mode = :clone
            cli_consume!(c)
        elseif arg == "--delete"
            mode = :delete
            cli_consume!(c)
        elseif arg == "--prune"
            mode = :prune
            cli_consume!(c)
        elseif arg == "--older-than"
            raw = strip(cli_take_value!(c, arg))
            n = tryparse(Int, raw)
            (n !== nothing && n >= 0) || throw(
                ArgumentError(
                    "--older-than needs a non-negative integer day count, got $(repr(raw))",
                )
            )
            older_days = n
        elseif arg == "--id"
            prune_id = String(strip(cli_take_value!(c, arg)))
            isempty(prune_id) && throw(ArgumentError("--id needs a non-empty token"))
        elseif arg == "--rsync"
            mode = :rsync_push
            cli_consume!(c)
        elseif arg == "--requirements"
            mode = :requirements
            cli_consume!(c)
        elseif arg == "--julia"
            julia_path = cli_take_value!(c, arg)
        elseif arg == "--repo"
            repo_url = String(strip(cli_take_value!(c, arg)))
        elseif arg in ("--remote-path", "--remote-dir")
            remote_path_override = String(strip(cli_take_value!(c, arg)))
        elseif arg == "--ignore-julia-version"
            ignore_julia_version = true
            cli_consume!(c)
        elseif cli_match(c, ["-h", "--help"])
            show_help = true
            cli_consume!(c)
        else
            let p = parse_placement_token(arg)
                push!(hosts, p.role === :parent ? PARENT_HOST_NAME : p.name)
            end
            cli_consume!(c)
        end
    end

    append_kit_host_sources!(hosts, cli_session; keep_counts = false, roles = true)
    apply_kit_cli_session!(cli_session)

    if (older_days !== nothing || prune_id !== nothing) && mode !== :prune
        throw(ArgumentError("--older-than / --id only apply to setup --prune"))
    end

    return (
        mode = mode,
        julia_path = julia_path,
        repo_url = repo_url,
        remote_path_override = remote_path_override,
        hosts = hosts,
        show_help = show_help,
        ignore_julia_version = ignore_julia_version,
        older_days = older_days,
        prune_id = prune_id,
        show_version = cli_session.show_version,
        cli_session = cli_session,
    )
end

show_usage(; io::IO = stdout) = show_requirements(; io)
setup_help_text()::String = sprint(io -> show_requirements(; io))
