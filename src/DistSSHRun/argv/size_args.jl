"""Pull `parent` out of the size host list into `include_parent`."""
function _size_absorb_parent_hosts!(hosts::Vector{String}, include_parent::Bool)::Bool
    kept = String[]
    inc = include_parent
    for h in hosts
        if is_parent_host_name(h)
            inc = true
        else
            push!(kept, h)
        end
    end
    empty!(hosts)
    append!(hosts, kept)
    return inc
end

function show_size_usage(; io::IO = stdout)
    print_help_chrome("DistSSHRun size"; io = io)
    print_help_lines(
        io,
        "Estimate worker counts from RAM and CPU.",
        "RSS = max(package load, optional --probe peak).",
    )
    print_help_blank(io)
    print_help_section("Usage"; io = io)
    print_help_lines(
        io,
        "  julia --project=. -m DistSSHKit size [parent] [child:NAME...]",
        "  size parent child:host1 child:host2",
        "  size --gb-per-worker 1.5 child:host1",
    )
    print_help_blank(io)
    print_help_section("Options"; io = io)
    print_help_lines(
        io,
        "  --gb-per-worker N   skip measure; assume N GB each",
        "  --probe PATH        warm-up script; peak RSS",
        "  --mem-headroom N    RAM fraction (default $(DEFAULT_MEM_HEADROOM))",
        "  --parent-gb N       parent process reserve (default $(DEFAULT_PARENT_GB))",
        "  --hosts CSV         parent / child:NAME[:N] (`:N` stripped)",
        "  --hosts-file PATH   same, one token per line",
        "  $(KIT_QUIET_FLAG_HELP)",
        "  $(KIT_PROGRESS_FLAG_HELP)",
        "  $(KIT_VERBOSE_FLAG_HELP)",
        "  --version, -v       print version and exit",
        "  -h, --help          this help",
    )
    print_help_blank(io)
    print_help_section("Environment"; io = io)
    print_help_lines(
        io,
        "  $(KIT_JOBS_ENV_HELP)",
    )
    print_help_blank(io)
    print_help_lines(
        io,
        "Details: docs (manual/size). See also: drive, setup.",
    )
    return nothing
end

function parse_size_args(args::Vector{String})
    cli_session, args = peel_kit_cli_flags(args)
    gb_per_worker = nothing
    probe = nothing
    mem_headroom = DEFAULT_MEM_HEADROOM
    parent_gb = DEFAULT_PARENT_GB
    include_parent = false
    hosts = String[]

    c = CliCursor(args)
    while !cli_at_end(c)
        arg = cli_current(c)::String
        if cli_match(c, ["-h", "--help"])
            cli_consume!(c)
            return (
                show_help = true,
                show_version = cli_session.show_version,
                cli_session = cli_session,
                gb_per_worker = gb_per_worker,
                probe = probe,
                mem_headroom = mem_headroom,
                parent_gb = parent_gb,
                include_parent = include_parent,
                hosts = hosts,
            )
        elseif cli_match(c, ["--local", "-l"]) || startswith(arg, "--local:") || startswith(arg, "-l:")
            throw_removed_local_flag(arg)
        elseif arg == "--parenthost" || arg == "--masterhost" || arg == "--parent"
            throw(
                ArgumentError(
                    "size: pass the token `parent` (e.g. size parent child:host1), not `--parenthost`.",
                )
            )
        elseif arg == "--gb-per-worker"
            gb_per_worker = parse(Float64, cli_take_value!(c, arg))
        elseif arg == "--probe"
            probe = String(cli_take_value!(c, arg))
        elseif arg == "--mem-headroom"
            mem_headroom = parse(Float64, cli_take_value!(c, arg))
        elseif arg == "--master-gb"
            throw(ArgumentError("size: use `--parent-gb N`, not `--master-gb`"))
        elseif arg == "--parent-gb"
            parent_gb = parse(Float64, cli_take_value!(c, arg))
        elseif !startswith(arg, "-")
            let p = parse_placement_token(arg)
                push!(hosts, p.role === :parent ? PARENT_HOST_NAME : p.name)
            end
            cli_consume!(c)
        else
            @warn "Unknown option: $arg (ignored)"
            cli_consume!(c)
        end
    end

    append_kit_host_sources!(hosts, cli_session; keep_counts = false, roles = true)
    include_parent = _size_absorb_parent_hosts!(hosts, include_parent)
    apply_kit_cli_session!(cli_session)

    if probe === nothing
        env_probe = strip(get(ENV, "DISTSSHKIT_SIZE_PROBE", ""))
        !isempty(env_probe) && (probe = String(env_probe))
    end

    return (
        show_help = false,
        show_version = cli_session.show_version,
        cli_session = cli_session,
        gb_per_worker = gb_per_worker,
        probe = probe,
        mem_headroom = mem_headroom,
        parent_gb = parent_gb,
        include_parent = include_parent,
        hosts = hosts,
    )
end
