# `go` — as-is complete job entry (setup assumed done; sync → exec → collect).

"""One execution slot for [`go!`](@ref) (parent or child)."""
struct GoSlot
    kind::Symbol # :parent | :child
    host::Union{Nothing, String}
    label::String
end

"""Outcome of [`go!`](@ref). On failure, `failed_step` is `"sync"`, `"run"`, or `"collect"`."""
struct GoResult
    ok::Bool
    sync::Union{Nothing, SyncResult}
    run::Union{Nothing, DriveResult}
    collect::Union{Nothing, CollectResult}
    script::String
    output_dir::String
    failed_step::Union{Nothing, String}
end

GoResult(
    ok::Bool,
    sync::Union{Nothing, SyncResult},
    run::Union{Nothing, DriveResult},
    collect::Union{Nothing, CollectResult},
    script::String,
    output_dir::String;
    failed_step::Union{Nothing, String} = nothing,
) = GoResult(ok, sync, run, collect, script, output_dir, failed_step)

function kit_run_result(
        result::GoResult,
        tokens::AbstractVector{<:AbstractString} = String[],
    )::KitRunResult
    return KitRunResult(
        result.ok,
        :go,
        _optional_path(result.output_dir),
        nothing,
        result.failed_step,
        _result_exit_code(result.ok, result.run, result.collect, result.failed_step),
        HostRunResult[],
        tokens,
    )
end

const _GO_IO_LOCK = ReentrantLock()

_go_is_parent_host(host_name::AbstractString)::Bool = is_parent_host_name(host_name)

"""Sanitize a host name for use as a directory component."""
function _go_sanitize_label(raw::AbstractString)::String
    s = replace(String(raw), r"[^A-Za-z0-9._@+-]+" => "_")
    return isempty(s) ? "host" : s
end

"""True when a go token looks like a misspelling of `parent`."""
function _go_local_host_typo_hint(host_name::AbstractString)::Union{Nothing, String}
    h = lowercase(String(host_name))
    h in (
        "lacal", "loacl", "locahost", "locl",
        "parenthos", "parnthost", "parent",
        "masterhost", "masterhos", "materhost",
    ) &&
        return "did you mean parent?"
    return nothing
end

"""
Build execution slots from host tokens.

- No tokens → one parent slot (directory label `parent`)
- `parent:N` → N slots on this job's DistSSHRun parent
- `parent:0` skips parent when children are listed
- `parent` / `child:NAME` without `:N` → error (except `--repeat`: uncapped)
- `child:NAME:N` → N SSH slots (`NAME`, or `NAME-1` … when N>1)
- `total=N` (`--repeat N`): N independent runs, round-robin across listed
  hosts. No tokens → all N on parent. Omit `:N` → no cap on that host.
  `parent:N` / `child:NAME:N` are per-host caps.
"""
function _go_merge_repeat_cap(
        a::Union{Nothing, Int},
        b::Union{Nothing, Int},
    )::Union{Nothing, Int}
    a === nothing && return nothing
    b === nothing && return nothing
    return a + b
end

"""Host pool for `--repeat`: first-seen order; omit `:N` → unlimited cap."""
struct GoRepeatHost
    role::Symbol
    name::String
    cap::Union{Nothing, Int}
end

function _go_repeat_pool(
        host_tokens::AbstractVector{<:AbstractString},
    )::Vector{GoRepeatHost}
    if isempty(host_tokens)
        return [GoRepeatHost(:parent, PARENT_HOST_NAME, nothing)]
    end
    order = Tuple{Symbol, String}[]
    caps = Dict{Tuple{Symbol, String}, Union{Nothing, Int}}()
    for raw in host_tokens
        p = parse_placement_token(String(raw))
        key = (p.role, p.name)
        cap = p.n
        if p.role === :parent
            cap === nothing || cap >= 0 || throw(
                ArgumentError(
                    "parent slot count must be >= 0, got $cap in $(repr(raw))",
                )
            )
        elseif something(cap, 1) < 1
            throw(ArgumentError("slot count must be >= 1, got $(something(cap, 1)) in $(repr(raw))"))
        end
        if !haskey(caps, key)
            push!(order, key)
            caps[key] = cap
        else
            caps[key] = _go_merge_repeat_cap(caps[key], cap)
        end
    end
    pool = GoRepeatHost[]
    for key in order
        cap = caps[key]
        cap === 0 && continue
        push!(pool, GoRepeatHost(key[1], key[2], cap))
    end
    return pool
end

function _go_tokens_for_repeat(
        host_tokens::AbstractVector{<:AbstractString},
        n::Int,
    )::Vector{String}
    pool = _go_repeat_pool(host_tokens)
    isempty(pool) && throw(
        ArgumentError(
            "no execution slots: list children after parent:0, or omit parent to run on the Kit side",
        )
    )
    assigned = zeros(Int, length(pool))
    start = 1
    nh = length(pool)
    for _ in 1:n
        placed = false
        for off in 0:(nh - 1)
            j = mod1(start + off, nh)
            cap = pool[j].cap
            if cap === nothing || assigned[j] < cap
                assigned[j] += 1
                start = mod1(j + 1, nh)
                placed = true
                break
            end
        end
        placed || throw(
            ArgumentError(
                "repeat $n exceeds per-host caps (`parent:N` / `child:NAME:N`)",
            )
        )
    end
    tokens = String[]
    for (h, k) in zip(pool, assigned)
        k == 0 && continue
        if h.role === :parent
            push!(tokens, format_placement_token(:parent, PARENT_HOST_NAME, k))
        else
            push!(tokens, format_placement_token(:child, h.name, k))
        end
    end
    return tokens
end

function _go_plan_slots(
        host_tokens::AbstractVector{<:AbstractString};
        total::Union{Nothing, Integer} = nothing,
    )::Vector{GoSlot}
    if total !== nothing
        n = Int(total)
        (total isa Bool || n < 1) && throw(ArgumentError("go repeat must be >= 1, got $total"))
        return _go_plan_slots(_go_tokens_for_repeat(host_tokens, n))
    end
    isempty(host_tokens) && return [GoSlot(:parent, nothing, PARENT_HOST_NAME)]

    parent_count = 0
    parent_label_base = PARENT_HOST_NAME
    remote_runs = Vector{String}() # host repeated per run
    for raw in host_tokens
        p = parse_placement_token(String(raw))
        n = something(p.n, 1)
        if p.role === :parent
            n < 0 &&
                throw(ArgumentError("parent slot count must be >= 0, got $n in $(repr(raw))"))
            parent_count += n
        elseif n < 1
            throw(ArgumentError("slot count must be >= 1, got $n in $(repr(raw))"))
        else
            for _ in 1:n
                push!(remote_runs, p.name)
            end
        end
    end

    parent_count == 0 && isempty(remote_runs) &&
        throw(
        ArgumentError(
            "no execution slots: list children after parent:0, or omit parent to run on the Kit side",
        )
    )

    slots = GoSlot[]
    if parent_count == 1 && isempty(remote_runs)
        push!(slots, GoSlot(:parent, nothing, parent_label_base))
    else
        for i in 1:parent_count
            label = parent_count == 1 ? parent_label_base : "$(parent_label_base)-$i"
            push!(slots, GoSlot(:parent, nothing, label))
        end
    end

    # Count runs per host for labels
    counts = Dict{String, Int}()
    totals = Dict{String, Int}()
    for h in remote_runs
        totals[h] = get(totals, h, 0) + 1
    end
    for h in remote_runs
        counts[h] = get(counts, h, 0) + 1
        i = counts[h]
        nlab = totals[h]
        base = _go_sanitize_label(h)
        label = nlab == 1 ? base : "$base-$i"
        push!(slots, GoSlot(:child, h, label))
    end
    return slots
end

function placement_tokens_from_go_slots(slots::Vector{GoSlot})::Vector{String}
    parent_n = count(s -> s.kind === :parent, slots)
    order = String[]
    counts = Dict{String, Int}()
    for s in slots
        s.kind === :child || continue
        h = s.host
        h isa String || continue
        if !haskey(counts, h)
            push!(order, h)
            counts[h] = 0
        end
        counts[h] += 1
    end
    out = String[]
    parent_n > 0 &&
        push!(out, format_placement_token(:parent, PARENT_HOST_NAME, parent_n))
    for h in order
        push!(out, format_placement_token(:child, h, counts[h]))
    end
    return out
end

function _go_script_relpath(project::AbstractString, script::AbstractString)::String
    proj = canonical_local_path(project)
    path = canonical_local_path(script)
    parent_prefix = joinpath(proj, "")
    startswith(path, parent_prefix) || path == proj ||
        throw(ArgumentError("script must be inside project ($proj): $path"))
    rel = relpath(path, proj)
    return rel == "." ? basename(path) : rel
end

"""`{script}/.distsshkit/go` when the script is in `project`, else `{project}/.distsshkit/go`."""
function _go_kit_parent(
        project::AbstractString,
        script::AbstractString,
    )::String
    proj = canonical_local_path(project)
    script_path = canonical_local_path(script)
    proj_prefix = joinpath(proj, "")
    dir = if startswith(script_path, proj_prefix) || dirname(script_path) == proj
        dirname(script_path)
    else
        proj
    end
    return kit_dir_beside_script(dir, :go)
end

"""Batch output directory: `{.distsshkit/go}/<stem>_<UTC>/` (`kit.progress` + slots).

Creates the leaf with exclusive `mkdir`. Same-second collisions get a
nanosecond suffix (`_mkdir_unique!`).
"""
function _go_batch_output_dir(
        project::AbstractString,
        script::AbstractString;
        now::DateTime = Dates.now(Dates.UTC),
    )::String
    stem = splitext(basename(canonical_local_path(script)))[1]
    stamp = Dates.format(now, dateformat"yyyymmddTHHMMSS") * "Z"
    dir = joinpath(_go_kit_parent(project, script), "$(stem)_$(stamp)")
    return _mkdir_unique!(dir)
end

function _go_host_ssh_hint(host::AbstractString)::String
    h = String(strip(host))
    if !contains(h, '@') && occursin(r"^(?:\d{1,3}\.){3}\d{1,3}$", h)
        return "hint: use user@$h (e.g. root@$h) if SSH defaults to your local username"
    end
    return ""
end

function _go_assert_remote_ready!(host::AbstractString, remote_root::AbstractString)
    rr = _remote_shell_path_word(remote_root)
    inner = "test -d $rr && test -f $rr/Project.toml"
    cmd = _ssh_cmd([ssh_opts()..., String(host), inner])
    out = IOBuffer()
    err = IOBuffer()
    proc = run(pipeline(ignorestatus(cmd), stdout = out, stderr = err), wait = true)
    if proc.exitcode != 0
        err_text = strip(String(take!(err)))
        ssh_hint = _go_host_ssh_hint(host)
        auth_or_conn = proc.exitcode == 255 ||
            occursin("Permission denied", err_text) ||
            occursin("Connection refused", err_text) ||
            occursin("Could not resolve hostname", err_text) ||
            occursin("No route to host", err_text)

        if auth_or_conn
            msg = "cannot SSH to $host"
            !isempty(err_text) && (msg *= ": $(first(split(err_text, '\n')))")
            !isempty(ssh_hint) && (msg *= "\n  $ssh_hint")
            throw(ArgumentError(msg))
        end

        msg = "remote project not ready on $host ($remote_root).\n" *
            "Run setup first (pick one deploy path), then instantiate:\n" *
            "  rsync:  julia --project=. -m DistSSHKit setup --rsync $(setup_cli_host_token(host))\n" *
            "  git:    julia --project=. -m DistSSHKit setup --clone $(setup_cli_host_token(host))\n" *
            "          (later updates: setup --sync $(setup_cli_host_token(host)))\n" *
            "  then:   julia --project=. -m DistSSHKit setup --instantiate $(setup_cli_host_token(host))\n" *
            "Or one-shot onto an empty path: go --rsync (instantiates missing deps).\n" *
            "Or from Julia: setup!(session, :rsync, :instantiate)\n" *
            "  (or go!(…; sync=:rsync); or :clone; repo=\"…\" then :instantiate; later :sync)\n" *
            "Set DISTRIBUTED_REMOTE_PROJECT_ROOT if the remote path is not the default."
        !isempty(ssh_hint) && (msg *= "\n  $ssh_hint")
        throw(ArgumentError(msg))
    end

    deps_err = probe_remote_project_deps(host, remote_root)
    if deps_err !== nothing
        throw(
            ArgumentError(
                "remote project deps not ready on $host ($remote_root): $deps_err\n" *
                    "Fix: julia --project=. -m DistSSHKit setup --instantiate $(setup_cli_host_token(host))\n" *
                    "  (or setup!(session, :instantiate) after :rsync / :clone)",
            )
        )
    end
    return nothing
end

function _go_assert_remotes_ready!(hosts::AbstractVector{<:AbstractString}, remote_root::AbstractString)
    errs = String[]
    for h in hosts
        try
            _go_assert_remote_ready!(h, remote_root)
        catch e
            push!(errs, sprint(showerror, e))
        end
    end
    isempty(errs) && return nothing
    throw(ArgumentError(join(errs, "\n\n")))
end

function _go_julia_exe()::String
    return resolve_controller_julia("auto")
end

"""Resolve Julia binary for `go!` (`nothing` / `"auto"` / empty → detect or local exe).

Remote auto-detect failures throw (no bare `"julia"` PATH fallback).
"""
function _go_resolve_julia(
        ::Nothing = nothing;
        host::Union{Nothing, AbstractString} = nothing,
    )::String
    host isa AbstractString || return _go_julia_exe()
    found = resolve_remote_julia(String(host), "auto")
    found === nothing && throw(
        ArgumentError(
            "Julia not found on remote host $(host) (auto-detect failed)",
        )
    )
    return found
end

function _go_resolve_julia(
        julia::AbstractString;
        host::Union{Nothing, AbstractString} = nothing,
    )::String
    s = strip(String(julia))
    (isempty(s) || lowercase(s) == "auto") && return _go_resolve_julia(nothing; host = host)
    if host isa AbstractString
        found = resolve_remote_julia(String(host), s)
        found === nothing && throw(
            ArgumentError(
                "Julia not usable on remote host $(host) at $(s)",
            )
        )
        return found
    end
    return resolve_controller_julia(s)
end

function _go_write_batch_manifest!(
        batch_dir::AbstractString,
        script::AbstractString,
        slots::Vector{GoSlot},
    )
    path = joinpath(batch_dir, "go_manifest.txt")
    open(path, "w") do io
        println(io, "script=", script)
        println(io, "slots=", length(slots))
        for (i, s) in enumerate(slots)
            host = s.host === nothing ? PARENT_HOST_NAME : s.host
            println(io, "slot[$i]=", s.label, " kind=", s.kind, " host=", host)
        end
    end
    return path
end

function _go_echo_script_log!(log_path::AbstractString)
    isfile(log_path) || return
    for line in eachline(log_path)
        writeln_both("  " * line)
    end
    return nothing
end

"""Replay each slot's `julia.stdout.log` after the live bar (`:progress` only)."""
function _print_go_slot_stdout_after_progress!(batch_dir::AbstractString)
    kit_output_progress() || return nothing
    logs = String[]
    isdir(batch_dir) || return nothing
    for (root, _, files) in walkdir(batch_dir)
        "julia.stdout.log" in files && push!(logs, joinpath(root, "julia.stdout.log"))
    end
    sort!(logs)
    isempty(logs) && return nothing
    many = length(logs) > 1
    for path in logs
        body = read(path, String)
        isempty(strip(body)) && continue
        if many
            println(stdout, relpath(dirname(path), batch_dir), ":")
        end
        print(stdout, body)
        endswith(body, '\n') || println(stdout)
    end
    return nothing
end

function _go_run_local_slot!(
        project::AbstractString,
        script::AbstractString,
        script_args::AbstractVector{<:AbstractString},
        slot_dir::AbstractString;
        quiet::Bool = false,
        julia::Union{Nothing, AbstractString} = nothing,
    )::DriveResult
    mkpath(slot_dir)
    log_path = joinpath(slot_dir, "julia.stdout.log")
    julia_bin = _go_resolve_julia(julia)
    argv = String[julia_bin, "--project=$(project)"]
    job_id = resolved_kit_job_id()
    if job_id !== nothing
        push!(argv, "-L", kit_write_job_mark_file(slot_dir, job_id))
    end
    push!(argv, String(script))
    append!(argv, collect(String, script_args))
    env_pairs = ["DISTRIBUTED_OUTPUT_DIR" => String(slot_dir)]
    job_id !== nothing && push!(env_pairs, "DISTSSHKIT_JOB_ID" => job_id)
    cmd = addenv(ignorestatus(Cmd(argv)), env_pairs...)
    proc = open(log_path, "w") do log
        return run(pipeline(cmd; stdout = log, stderr = log); wait = true)
    end
    # Mirror script stdout in `:verbose` only (quiet/progress own the TTY).
    if !quiet && kit_output_detail() && isfile(log_path)
        lock(_GO_IO_LOCK) do
            _go_echo_script_log!(log_path)
            writeln_both("")
        end
    end
    code = proc.exitcode isa Integer ? Int(proc.exitcode) : 1
    return DriveResult(code == 0, code; output_dir = slot_dir)
end

"""Remote SSH shell snippet for one `go` slot (cd project root before mkdir/log paths)."""
function _go_remote_slot_shell_inner(
        remote_root::AbstractString,
        slot_rel::AbstractString,
        script_rel::AbstractString,
        script_args::AbstractVector{<:AbstractString},
        julia_bin::AbstractString,
    )::String
    rr = _remote_shell_path_word(remote_root)
    rel_q = _remote_shell_path_word(script_rel)
    slot_q = _remote_shell_path_word(slot_rel)
    # `join(xs, delim)` infers `Union{String,Nothing}` (IO method) on 1.13.
    args_s = sprint() do io
        for a in script_args
            print(io, ' ', _remote_shell_path_word(a))
        end
    end
    jb = _remote_shell_path_word(julia_bin)
    log_q = _remote_shell_path_word(joinpath(slot_rel, "julia.stdout.log"))
    job_id = resolved_kit_job_id()
    job_export = ""
    job_load = ""
    job_mark = ""
    if job_id !== nothing
        mark_rel = joinpath(slot_rel, kit_job_pkill_pattern(job_id))
        mark_q = _remote_shell_path_word(mark_rel)
        # `#…` must be single-quoted: `_remote_shell_path_word` leaves it raw, and
        # an unquoted word starting with `#` is a shell comment.
        comment_q = "'" * kit_job_mark_comment(job_id) * "'"
        job_export = "export DISTSSHKIT_JOB_ID=$(_remote_shell_path_word(job_id)) && "
        job_mark = "printf '%s\\n' $comment_q > $mark_q && "
        job_load = " -L $mark_q"
    end
    return string(
        "cd $rr && mkdir -p $slot_q && ",
        "export DISTRIBUTED_OUTPUT_DIR=$slot_q && ",
        job_export,
        job_mark,
        "$jb$job_load --project=. $rel_q$args_s >$log_q 2>&1; ",
        "ec=\$?; echo \$ec > $slot_q/go.exitcode; cat $log_q; exit \$ec",
    )
end

function _go_run_remote_slot!(
        host::AbstractString,
        project::AbstractString,
        remote_root::AbstractString,
        script::AbstractString,
        script_args::AbstractVector{<:AbstractString},
        slot_rel::AbstractString,
        slot_dir::AbstractString;
        quiet::Bool = false,
        julia::Union{Nothing, AbstractString} = nothing,
    )::DriveResult
    mkpath(slot_dir)
    rel = _go_script_relpath(project, script)
    julia_bin = _go_resolve_julia(julia; host = String(host))
    inner = _go_remote_slot_shell_inner(
        remote_root, slot_rel, rel, script_args, julia_bin,
    )
    cmd = ignorestatus(_ssh_cmd([ssh_opts()..., String(host), inner]))
    # Capture streams ourselves: piping to the parent's stdout can drop ssh exit codes.
    buf = IOBuffer()
    proc = run(pipeline(cmd; stdout = buf, stderr = buf); wait = true)
    out = String(take!(buf))
    # `:verbose` only: quiet/progress suppress script echo (still in slot logs).
    if !quiet && kit_output_detail() && !isempty(out)
        lock(_GO_IO_LOCK) do
            write(stdout, out)
        end
    end
    ssh_code = proc.exitcode isa Integer ? Int(proc.exitcode) : 1
    # Prefer the remote-written exit file when ssh status is unreliable.
    ec_path = joinpath(slot_dir, "go.exitcode")
    scp_failed = false
    try
        remote_ec = string(rstrip(remote_root, '/'), "/", slot_rel, "/go.exitcode")
        run(
            pipeline(
                _scp_cmd([ssh_opts()..., string(host, ":", remote_ec), ec_path]);
                stdout = devnull,
                stderr = devnull,
            );
            wait = true,
        )
    catch e
        _rethrow_missing_host_tool(e)
        scp_failed = true
    end
    code = _go_slot_exitcode(ssh_code, ec_path; scp_failed)
    return DriveResult(code == 0, code; output_dir = slot_dir)
end

"""Prefer `go.exitcode` when present. If scp failed, ignore a local file (may be stale)."""
function _go_slot_exitcode(
        ssh_code::Int,
        ec_path::AbstractString;
        scp_failed::Bool,
    )::Int
    if !scp_failed && isfile(ec_path)
        parsed = tryparse(Int, strip(read(ec_path, String)))
        parsed !== nothing && return parsed
    end
    return scp_failed ? 1 : ssh_code
end

"""Rsync one remote slot directory into the local slot directory (slot-overwrite collect)."""
function _go_pull_slot!(
        host::AbstractString,
        remote_root::AbstractString,
        slot_rel::AbstractString,
        slot_dir::AbstractString,
    )::Bool
    mkpath(slot_dir)
    remote_slot = joinpath(remote_root, slot_rel)
    # Ensure trailing slash semantics: copy contents into slot_dir
    src = remote_slot * "/"
    dest = slot_dir * "/"
    rsync = _host_sync_rsync_argv()
    transport = _host_sync_rsync_transport()
    cmd = ignorestatus(
        Cmd(
            vcat(
                rsync,
                ["-az", "-e", transport, "$(host):$src", dest],
            ),
        ),
    )
    try
        proc = run(pipeline(cmd; stdout = devnull, stderr = devnull); wait = true)
        return proc.exitcode == 0
    catch e
        _rethrow_missing_host_tool(e)
        return false
    end
end

"""Run one slot (local process or remote SSH). Isolated env per slot. Pull is `_go_collect_slot!`."""
function _go_exec_slot!(
        slot::GoSlot,
        proj::AbstractString,
        script_path::AbstractString,
        args::AbstractVector{<:AbstractString},
        batch_dir::AbstractString,
        remote_root::AbstractString;
        quiet::Bool = false,
        julia::Union{Nothing, AbstractString} = nothing,
    )
    slot_dir = joinpath(batch_dir, slot.label)
    mkpath(slot_dir)
    run_lab = string(slot.label, "/run")
    _kit_progress_span!(run_lab, :running)
    if slot.kind === :parent
        run_res = _go_run_local_slot!(
            proj, script_path, args, slot_dir; quiet = quiet, julia = julia,
        )
    else
        host = slot.host::String
        slot_rel = relpath(slot_dir, proj)
        run_res = _go_run_remote_slot!(
            host,
            proj,
            remote_root,
            script_path,
            args,
            slot_rel,
            slot_dir;
            quiet = quiet,
            julia = julia,
        )
    end
    _kit_progress_span!(run_lab, run_res.ok ? :ok : :fail)
    return (run = run_res, collect = nothing, collect_fail = false)
end

"""Rsync one successful remote slot after every script has finished."""
function _go_collect_slot!(
        slot::GoSlot,
        proj::AbstractString,
        batch_dir::AbstractString,
        remote_root::AbstractString,
    )
    slot.kind === :child || return (collect = nothing, collect_fail = false)
    host = slot.host::String
    slot_dir = joinpath(batch_dir, slot.label)
    slot_rel = relpath(slot_dir, proj)
    col_lab = string(slot.label, "/collect")
    _kit_progress_span!(col_lab, :running)
    if _go_pull_slot!(host, remote_root, slot_rel, slot_dir)
        _kit_progress_span!(col_lab, :ok)
        return (collect = CollectResult(true, 0), collect_fail = false)
    end
    _kit_progress_span!(col_lab, :fail)
    return (collect = CollectResult(false, 1), collect_fail = true)
end

"""Log header matching drive: subcommand args, Julia env, then the go banner."""
function _go_print_run_header!(
        original_args::Vector{String},
        script_path::AbstractString,
        script_args::AbstractVector{<:AbstractString},
        slots::Vector{GoSlot},
        proj::AbstractString,
        anchor::AbstractString,
    )
    writeln_field("Subcommand args", subcommand_args_record("go", original_args))
    for (label, value) in julia_env_record()
        writeln_field(label, value)
    end
    writeln_both("")
    print_header("DistSSHRun go")
    writeln_both("")
    writeln_field("Script", display_path(script_path, anchor))
    writeln_field("Args", isempty(script_args) ? "—" : join(script_args, " "))
    writeln_field("Project", cli_project_disp(proj, anchor))
    writeln_field("DistSSHRun", dist_ssh_kit_version())
    app_git = get_local_git_hash(proj; short = 8)
    writeln_field("App git", app_git === nothing ? "unavailable" : app_git)
    writeln_field("Slots", string(length(slots)))
    for s in slots
        where = s.kind === :parent ? PARENT_HOST_NAME : String(something(s.host, s.label))
        writeln_both("  · $(s.label)  ($where)"; color = :light_black)
    end
    writeln_both("")
    return nothing
end

function _go_complete!(
        result::GoResult,
        batch_dir::AbstractString,
        release_lock,
        progress_ok::Bool,
        anchor,
        tokens::Vector{String},
    )::GoResult
    footer = progress_ok ? display_path(batch_dir, anchor) : nothing
    kit_progress_done!(; ok = progress_ok, footer = footer)
    _print_go_slot_stdout_after_progress!(batch_dir)
    _maybe_print_kit_progress_phases(batch_dir)
    close_log_file()
    _set_kit_progress_sidecar!(nothing)
    kr = kit_run_result(result, tokens)
    _write_kit_result_file(kr)
    rd = kit_run_dir()
    if rd !== nothing
        write_kit_run_toml!(
            rd;
            kind = :go,
            output_dir = batch_dir,
            log_dir = batch_dir,
            result = kr,
        )
    end
    release_lock()
    _remove_kit_pid_file(getpid(), batch_dir, nothing)
    return result
end

"""
    go!(script, workers...; kwargs...)
    go!(script, workers::AbstractVector; kwargs...)

Run an as-is complete job on one or more slots (local and/or remote).

```julia
go!("job.jl")                          # one parent slot
go!("job.jl"; repeat=100)              # 100 independent runs on parent
go!("job.jl", "parent:2"; args=["8"])
go!("job.jl", "child:user@h1:1", "child:user@h2:1"; remote="/path/to/project")
```

Each slot gets `DISTRIBUTED_OUTPUT_DIR` pointing at
`{script}/.distsshkit/go/<stem>_<UTC>/<slot>/`.
Override the batch root with `output_dir` (CLI: `--output-dir`). For backward
compatibility `collect_spec::AbstractString` also sets the batch root, but passing
both `output_dir` and `collect_spec::String` is an error. `collect_spec === false`
means "skip collect" and is orthogonal to `output_dir`.

Default `sync` is `false` (no pre-run sync; remotes are checked first — setup
is assumed done). Pass `sync=:sync` or `sync=:rsync` to copy, then check. After `:rsync`,
hosts that still lack Manifest deps get [`instantiate!`](@ref) before the
check. Use `sync=:rsync` only onto a missing/empty remote path (or
`setup --delete` / `setup!(session, :delete)` first). `go!` has no git-parity
gate; use [`drive!`](@ref) with `skip_hash_check=false` (CLI: `drive --require-git`)
when you need that.

`julia` sets the Julia binary for each slot (`nothing` / `"auto"` → detect;
same as CLI `--julia`).

When `workers` is empty and `hosts_file` is omitted, `DISTSSHKIT_HOSTS_FILE`
is still read (API `go!("job.jl")`). Non-empty `workers` (CLI
[`host_tokens`](@ref)) does not re-read that ENV.

`parent:N` and `child:NAME:N` mean N independent full-job runs (not Distributed workers),
started together. `repeat=N` (CLI `--repeat N`) is the total number of those
runs, spread round-robin across listed hosts (no tokens → all on parent).
`:N` on a token is a per-host cap when `repeat` is set; omit it to leave that
host uncapped. Without `repeat`, every listed token needs `:N` (omitting it is
not 1 and not [`size!`](@ref)). Empty tokens stay one parent slot.
`path_anchor` shortens displayed paths (CLI passes kit project root).
"""
function go!(
        script::AbstractString,
        workers::AbstractVector{<:AbstractString};
        project::AbstractString = pwd(),
        remote::Union{Nothing, AbstractString} = nothing,
        hosts_file::Union{Nothing, AbstractString} = nothing,
        quiet::Bool = false,
        verbosity::Union{Nothing, Symbol} = nothing,
        yes::Bool = true,
        sync::Union{Symbol, Bool, Nothing} = nothing,
        output_dir::Union{Nothing, AbstractString} = nothing,
        collect_spec::Union{Bool, AbstractString, Nothing} = nothing,
        args::AbstractVector{<:AbstractString} = String[],
        path_anchor::Union{Nothing, AbstractString} = nothing,
        julia::Union{Nothing, AbstractString} = nothing,
        hint_surface::Symbol = :api,
        original_args::Vector{String} = String[],
        repeat::Union{Nothing, Integer} = nothing,
    )::GoResult
    script_path = canonical_local_path(script)
    proj = canonical_local_path(project)
    if !isfile(script_path)
        throw(
            ArgumentError(
                explain_script_not_found(
                    script_path,
                    proj;
                    surface = hint_surface,
                    headline = "script not found: $script_path",
                )
            )
        )
    end
    _acquire_kit_inproc_run!(:go)
    old_run = get(ENV, DISTSSHKIT_RUN_DIR_ENV, nothing)
    try
        _ensure_kit_run_dir!(:go, script_path; project = proj)
        return _go_run!(
            script_path,
            proj,
            workers,
            remote,
            hosts_file,
            quiet,
            verbosity,
            yes,
            sync,
            output_dir,
            collect_spec,
            args,
            path_anchor,
            julia,
            hint_surface,
            original_args,
            repeat,
        )
    finally
        _restore_kit_run_dir_env!(old_run)
        _release_kit_inproc_run!()
    end
end

function _go_run!(
        script_path::String,
        proj::String,
        workers,
        remote,
        hosts_file,
        quiet,
        verbosity,
        yes,
        sync,
        output_dir,
        collect_spec,
        args,
        path_anchor,
        julia,
        hint_surface,
        original_args,
        repeat,
    )
    anchor = something(path_anchor, proj)

    tokens = String[String(h) for h in workers]
    hf = hosts_file
    # CLI / [`host_tokens`](@ref) already merged `--hosts-file` / ENV into
    # `workers`. Re-reading `DISTSSHKIT_HOSTS_FILE` would duplicate slots.
    # API `go!("job.jl")` with empty `workers` still honors the ENV file.
    if hf === nothing && isempty(tokens)
        env_hf = strip(get(ENV, "DISTSSHKIT_HOSTS_FILE", ""))
        !isempty(env_hf) && (hf = env_hf)
    end
    if hf !== nothing && !isempty(strip(String(hf)))
        for line in read_hosts_file_lines(hf; surface = hint_surface)
            push!(tokens, line)
        end
    end
    if repeat === nothing
        require_counted_placement_tokens(tokens; surface = hint_surface)
    end
    slots = _go_plan_slots(tokens; total = repeat)
    place = placement_tokens_from_go_slots(slots)
    if output_dir !== nothing && collect_spec isa AbstractString
        throw(
            ArgumentError(
                "go!: set the batch root via output_dir OR collect_spec::String, not both",
            )
        )
    end
    batch_dir = if output_dir !== nothing
        canonical_local_path(output_dir)
    elseif collect_spec isa AbstractString
        canonical_local_path(collect_spec)
    else
        _go_batch_output_dir(proj, script_path)
    end
    mkpath(batch_dir)
    release_lock = kit_output_dir_lock!(batch_dir)
    _go_write_batch_manifest!(batch_dir, script_path, slots)
    _set_kit_progress_sidecar!(batch_dir)
    apply_session_env!(
        KitSession(
            project = proj,
            workers = String[],
            remote = remote,
            quiet = quiet,
            verbosity = verbosity,
            yes = yes,
        ),
    )
    init_log_file(batch_dir; prefix = "go", path_anchor = anchor)

    progress_ok = false
    completed = false
    try
        go_session = KitSession(
            project = proj,
            workers = String[],
            remote = remote,
            quiet = quiet,
            verbosity = verbosity,
            yes = yes,
        )
        sess_rr = session_remote_root(go_session)

        child_hosts = unique(String[s.host for s in slots if s.kind === :child])
        _write_kit_hosts_file(child_hosts, batch_dir, nothing)

        skip_collect = collect_spec === false
        any_run_fail = Ref(false)
        any_collect_fail = Ref(false)
        last_run = Ref(DriveResult(true, 0))
        last_collect = Ref{Union{Nothing, CollectResult}}(nothing)

        _go_print_run_header!(original_args, script_path, args, slots, proj, anchor)

        n_slots = length(slots)
        if n_slots > 0
            kit_progress_begin!(
                "go";
                steps = n_slots,
                items = String[s.label for s in slots],
                kind = :go,
            )
            _kit_progress_mark!("ready")
        end
        sync_result = nothing
        sync_mode = something(sync, false)
        n_slots > 0 && _kit_progress_mark!("sync")
        # `sync=false`: remotes must already have Project.toml + deps.
        # With `:rsync` / `:sync`, copy first (empty `~/jobs/<id>` is expected).
        if sync_mode === false
            _go_assert_remotes_ready!(child_hosts, sess_rr)
        elseif !isempty(child_hosts)
            sync_session = KitSession(
                project = proj,
                workers = [format_placement_token(:child, h) for h in child_hosts],
                remote = remote,
                quiet = quiet,
                verbosity = verbosity,
                yes = yes,
            )
            sync_result = sync!(sync_session; mode = sync_mode)
            if !sync_result.ok
                completed = true
                return _go_complete!(
                    GoResult(
                        false,
                        sync_result,
                        nothing,
                        nothing,
                        script_path,
                        batch_dir;
                        failed_step = "sync",
                    ),
                    batch_dir,
                    release_lock,
                    false,
                    anchor,
                    place,
                )
            end
            if sync_mode === :rsync
                inst_julia = julia === nothing || strip(String(julia)) == "auto" ?
                    "auto" : String(julia)
                inst = instantiate_after_rsync!(sync_session; julia = inst_julia)
                if inst !== nothing && !inst.ok
                    completed = true
                    return _go_complete!(
                        GoResult(
                            false,
                            inst,
                            nothing,
                            nothing,
                            script_path,
                            batch_dir;
                            failed_step = "instantiate",
                        ),
                        batch_dir,
                        release_lock,
                        false,
                        anchor,
                        place,
                    )
                end
            end
            _go_assert_remotes_ready!(child_hosts, sess_rr)
        end

        n_slots > 0 && _kit_progress_mark!("run")
        slot_run_ok = fill(false, n_slots)
        @sync for (i, slot) in enumerate(slots)
            @async begin
                kit_progress_item!(slot.label; status = :running)
                err = nothing
                outcome = try
                    _go_exec_slot!(
                        slot,
                        proj,
                        script_path,
                        args,
                        batch_dir,
                        sess_rr;
                        quiet = quiet,
                        julia = julia,
                    )
                catch e
                    err = e
                    (
                        run = DriveResult(false, 1),
                        collect = nothing,
                        collect_fail = false,
                    )
                end
                slot_run_ok[i] = outcome.run.ok
                lock(_GO_IO_LOCK) do
                    last_run[] = outcome.run
                    if err !== nothing
                        any_run_fail[] = true
                        kit_progress_item!(slot.label; status = :fail)
                        write(stderr, "  ")
                        print_err("✗ $(slot.label): $(sprint(showerror, err))"; io = stderr)
                        println(stderr)
                    elseif !outcome.run.ok
                        any_run_fail[] = true
                        kit_progress_item!(slot.label; status = :fail)
                        write(stderr, "  ")
                        print_err("✗ $(slot.label) (exit $(outcome.run.exit_code))"; io = stderr)
                        println(stderr)
                    else
                        kit_progress_item!(slot.label; status = :ok)
                        ok(slot.label)
                    end
                end
            end
        end

        n_slots > 0 && _kit_progress_mark!("collect")
        if !skip_collect
            @sync for (i, slot) in enumerate(slots)
                slot_run_ok[i] || continue
                slot.kind === :child || continue
                @async begin
                    err = nothing
                    outcome = try
                        _go_collect_slot!(
                            slot, proj, batch_dir, sess_rr,
                        )
                    catch e
                        err = e
                        (collect = CollectResult(false, 1), collect_fail = true)
                    end
                    lock(_GO_IO_LOCK) do
                        if outcome.collect !== nothing
                            last_collect[] = outcome.collect
                        end
                        if outcome.collect_fail || err !== nothing
                            any_collect_fail[] = true
                        end
                        if err !== nothing
                            write(stderr, "  ")
                            print_err(
                                "✗ $(slot.label): $(sprint(showerror, err))";
                                io = stderr,
                            )
                            println(stderr)
                        end
                    end
                end
            end
        end
        if any_run_fail[]
            completed = true
            return _go_complete!(
                GoResult(
                    false,
                    sync_result,
                    last_run[],
                    last_collect[],
                    script_path,
                    batch_dir;
                    failed_step = "run",
                ),
                batch_dir,
                release_lock,
                false,
                anchor,
                place,
            )
        end
        if any_collect_fail[]
            completed = true
            return _go_complete!(
                GoResult(
                    false,
                    sync_result,
                    last_run[],
                    last_collect[],
                    script_path,
                    batch_dir;
                    failed_step = "collect",
                ),
                batch_dir,
                release_lock,
                false,
                anchor,
                place,
            )
        end

        writeln_both("")
        writeln_both("Results: $(display_path(batch_dir, anchor))")
        progress_ok = true
        completed = true
        return _go_complete!(
            GoResult(true, sync_result, last_run[], last_collect[], script_path, batch_dir),
            batch_dir,
            release_lock,
            true,
            anchor,
            place,
        )
    finally
        if !completed
            footer = progress_ok ? display_path(batch_dir, anchor) : nothing
            kit_progress_done!(; ok = progress_ok, footer = footer)
            _print_go_slot_stdout_after_progress!(batch_dir)
            _maybe_print_kit_progress_phases(batch_dir)
            close_log_file()
            _set_kit_progress_sidecar!(nothing)
            release_lock()
            _remove_kit_pid_file(getpid(), batch_dir, nothing)
        end
    end
end

function go!(script::AbstractString; kwargs...)::GoResult
    return go!(script, String[]; kwargs...)
end

function go!(
        script::AbstractString,
        w1::AbstractString,
        rest::AbstractString...;
        kwargs...,
    )::GoResult
    return go!(script, String[w1, rest...]; kwargs...)
end

"""
    report_go_errors(result::GoResult; io=stderr)

Print a short summary when [`go!`](@ref) failed. Returns `result.ok`.
"""
function report_go_errors(result::GoResult; io::IO = stderr)::Bool
    result.ok && return true
    _report_run_header!(io, kit_run_result(result))
    _report_sync_host_errors!(io, result.sync)
    if result.run !== nothing && !result.run.ok
        println(io, "  run exit $(result.run.exit_code)")
    end
    if result.collect !== nothing && !result.collect.ok
        println(io, "  collect exit $(result.collect.exit_code)")
    end
    return false
end

function report_run_errors(result::GoResult; io::IO = stderr)::Bool
    return report_go_errors(result; io = io)
end
