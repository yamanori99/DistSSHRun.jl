#!/usr/bin/env julia
"""
`julia -m DistSSHKit setup` — deploy and verify the project on SSH hosts.

Recommended workflow:
  1. --rsync → 2. --instantiate → 3. --check → 4. --runtest (optional) → `go` / `drive`
  Optional git: --clone → --instantiate → --check → --sync / --pull
  Replace a remote tree: --delete, then --rsync or --clone

  julia --project=. -m DistSSHKit setup --rsync child:host1 child:host2
  julia --project=. -m DistSSHKit setup --sync child:host1 child:host2   # git updates

See `--help`.
"""

# Guard on a setup-only import — not names `go`/`drive` may already have
# bound from DistSSHRun (e.g. `cli_project_root`) before `setup.jl` is included.
# Top-level include so JETLS sees `_include_checkout_run` (it does not follow
# `include` inside `catch`).
include(joinpath(@__DIR__, "_checkout.jl"))

if !isdefined(@__MODULE__, :DistSSHRun)
    if get(ENV, "DIST_SSH_KIT_CLI_INCLUDE", "") == "1"
        import DistSSHRun
    else
        try
            import DistSSHRun
        catch
            include(joinpath(@__DIR__, "_checkout.jl"))
            _include_checkout_run()
        end
    end
end

# `using DistSSHBase` (test support) already binds `resolve_remote_project_root`.
# Key off the setup parser, which that import does not bring in.
if !isdefined(@__MODULE__, :parse_setup_args)
    include(joinpath(@__DIR__, "setup", "_using.jl"))
end

if !isdefined(@__MODULE__, :setup_main)
    function setup_main()::Cint
        opts = try
            parse_setup_args(ARGS)
        catch e
            e isa ArgumentError || rethrow()
            print_err("Error: "; bold = true)
            println_fatal(e.msg)
            println_fatal()
            show_usage()
            return 1
        end

        if opts.show_version
            DistSSHRun.println_kit_version()
            return 0
        end

        if opts.show_help
            show_usage()
            return 0
        end

        if opts.mode === nothing || opts.mode === :requirements
            show_requirements()
            return 0
        end

        try
            validate_setup_hosts(
                opts.hosts;
                allow_parent = setup_mode_allows_parent(opts.mode::Symbol),
            )
        catch e
            e isa ArgumentError || rethrow()
            # Fatal: always on terminal.
            print_err("Error: "; bold = true)
            println_fatal(e.msg)
            println_fatal()
            show_usage()
            return 1
        end

        project = String(DistSSHRun.cli_project_root(@__DIR__))
        path_anchor = DistSSHRun.canonical_local_path(project)
        remote_path = resolve_remote_project_root(
            project;
            cli_override = opts.remote_path_override,
        )
        deploy_path = DistSSHRun.remote_deploy_root(
            project;
            cli_override = opts.remote_path_override,
        )

        function setup_job!(mode::Symbol)::Cint
            mode_name = Dict(
                :clone => "Clone",
                :delete => "Delete",
                :check => "Check Prerequisites",
                :pull => "Pull",
                :sync => "Sync",
                :rsync_push => "rsync (no git)",
                :instantiate => "Instantiate",
                :runtest => "Pkg.test (job)",
                :cleanup => "Cleanup Workers",
                :prune => "Prune kit leaves",
            )[mode]
            print_header("$(DistSSHRun.cli_heading("setup")) · $mode_name")
            kit_println()
            writeln_field("Remote path", remote_path)
            kit_println()

            # Mutating / multi-host SSH ops: fail fast before confirmations.
            if mode === :delete || mode === :clone || mode === :rsync_push ||
                    mode === :instantiate || mode === :runtest || mode === :prune
                if !preflight_setup_ssh(opts.hosts)
                    print_err("SSH preflight failed. Fix connectivity, then retry.")
                    kit_println()
                    return Cint(1)
                end
            elseif setup_mode_allows_parent(mode)
                ssh_hosts = setup_juliaup_ssh_hosts(opts.hosts)
                if !isempty(ssh_hosts) && !preflight_setup_ssh(ssh_hosts)
                    print_err("SSH preflight failed. Fix connectivity, then retry.")
                    kit_println()
                    return Cint(1)
                end
            end

            if mode === :delete
                delete_path = DistSSHRun.remote_delete_root(
                    project;
                    cli_override = opts.remote_path_override,
                )
                return Cint(finish_host_op!("Delete", delete_remotes(opts.hosts, delete_path)) ? 0 : 1)
            end

            if mode === :clone
                DistSSHRun.ensure_manifest_in_git_worktree!(project)
                clone_url = resolve_clone_url(opts.repo_url, project)
                clone_dest = DistSSHRun.remote_git_clone_dest(
                    project;
                    cli_override = opts.remote_path_override,
                )
                result = clone_to_remotes(opts.hosts, clone_dest, clone_url)
                ok = finish_host_op!("Clone", result)
                if ok && !result.cancelled && result.failed == 0 &&
                        (
                        opts.remote_path_override !== nothing ||
                            !isempty(strip(get(ENV, "DISTRIBUTED_REMOTE_PROJECT_ROOT", "")))
                    )
                    kit_println("  Tip: export DISTRIBUTED_REMOTE_PROJECT_ROOT=$deploy_path")
                    kit_println("       so drive.jl uses the same remote root for workers / collect.")
                    kit_println()
                end
                return Cint(ok ? 0 : 1)
            end

            if mode === :rsync_push
                return Cint(
                    finish_host_op!(
                            "rsync",
                            rsync_push_to_remotes(opts.hosts, deploy_path, project; path_anchor = path_anchor),
                        ) ? 0 : 1
                )
            end

            if mode === :instantiate
                return Cint(
                    finish_host_op!(
                            "Instantiate",
                            instantiate_remotes(
                                opts.hosts, opts.julia_path, remote_path, project;
                                path_anchor = path_anchor,
                            ),
                        ) ? 0 : 1
                )
            end

            if mode === :runtest
                return Cint(
                    finish_host_op!(
                            "Pkg.test",
                            runtest_remotes(
                                opts.hosts, opts.julia_path, remote_path, project;
                                path_anchor = path_anchor,
                            ),
                        ) ? 0 : 1
                )
            end

            if mode === :cleanup
                return Cint(finish_host_op!("Cleanup", cleanup_remote_workers(opts.hosts)) ? 0 : 1)
            end

            if mode === :prune
                return Cint(
                    finish_host_op!(
                            "Prune",
                            prune_kit_leaves(
                                opts.hosts,
                                remote_path,
                                project;
                                older_days = opts.older_days,
                                id = opts.prune_id,
                                skip_setup = DistSSHRun.setup_log_dir(project),
                            ),
                        ) ? 0 : 1
                )
            end

            # --pull/--sync: allow commit mismatch (fixed by the op). --check: require sync.
            # --sync also requires a clean local tree.
            require_clean = (mode === :sync)
            check_code_sync = (mode === :check)
            result = check_prerequisites(
                opts.hosts, opts.julia_path, remote_path, project;
                path_anchor = path_anchor,
                require_clean_git = require_clean,
                check_code_sync = check_code_sync,
                ignore_julia_version = opts.ignore_julia_version,
            )

            if !result.ok
                print_err("Prerequisites not met. Fix issues above and retry.")
                kit_println()
                return Cint(1)
            end

            if mode === :check
                print_ok("All prerequisites met.")
                kit_println()
                return Cint(0)
            end

            if !result.needs_sync
                print_ok("Already up to date.")
                kit_println()
                return Cint(0)
            end

            print_ok("Ready to proceed.")
            kit_println()
            kit_println()

            # --pull: pull on localhost first, then on remotes
            # --sync: push from localhost, then pull on remotes
            do_push = (mode === :sync)
            do_local_pull = (mode === :pull)
            raw = git_sync_project_to_hosts!(
                opts.hosts,
                project,
                remote_path;
                do_push = do_push,
                do_pull = true,
                do_local_pull = do_local_pull,
            )
            raw.cancelled && return Cint(0)
            if !raw.ok
                print_err("$mode_name failed.")
                kit_println()
                return Cint(1)
            end

            print_ok("$mode_name complete.")
            kit_println()
            return Cint(0)
        end

        log_dir = DistSSHRun.setup_log_dir(project)
        init_log_file(
            log_dir;
            prefix = "setup",
            path_anchor = path_anchor,
        )
        try
            return DistSSHRun.with_kit_setup_progress(
                log_dir,
                DistSSHRun.setup_progress_step_name(opts.mode::Symbol);
                path_anchor = path_anchor,
            ) do
                setup_job!(opts.mode::Symbol)
            end
        finally
            close_log_file()
        end
    end
end # setup_main guard

if get(ENV, "DIST_SSH_KIT_CLI_INCLUDE", "") != "1" &&
        !isempty(PROGRAM_FILE) &&
        abspath(PROGRAM_FILE) == abspath(@__FILE__)
    exit(setup_main())
end
