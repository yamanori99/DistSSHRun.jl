using Test

@testset "ride" begin
    @testset "activate project" begin
        prev = Base.active_project()
        _with_tempdir() do tmp
            write(
                joinpath(tmp, "Project.toml"),
                """
                name = "RideAct"
                uuid = "aaaaaaaa-bbbb-cccc-dddd-eeeeeeeeeeee"
                version = "0.1.0"
                """,
            )
            try
                DistSSHRun._ride_activate_project!(tmp)
                @test startswith(Base.active_project(), tmp)
            finally
                DistSSHRun._ride_restore_project!(prev)
            end
        end
        @test Base.active_project() == prev
    end

    @testset "rewrite" begin
        ex = Meta.parse("map(f, xs)")
        rw = DistSSHRun._ride_rewrite(ex)
        @test rw.head === :call
        @test rw.args[1] === GlobalRef(DistSSHRun, :_ride_map)
        @test rw.args[2].head === :call
        @test rw.args[2].args[1] === GlobalRef(DistSSHRun, :_ride_named_fn)

        fx = Meta.parse("filter(iseven, xs)")
        @test DistSSHRun._ride_rewrite(fx).args[1] === GlobalRef(DistSSHRun, :_ride_filter)

        mx2 = Meta.parse("map(+, xs, ys)")
        @test DistSSHRun._ride_rewrite(mx2).args[1] === :map

        cx = Meta.parse("[x^2 for x in xs]")
        cr = DistSSHRun._ride_rewrite(cx)
        @test cr.args[1] === GlobalRef(DistSSHRun, :_ride_map)

        fx2 = Meta.parse("for i in xs; f(i); end")
        @test DistSSHRun._ride_rewrite(fx2).head === :for

        fill = Meta.parse("for i in eachindex(xs); ys[i] = xs[i] * xs[i]; end")
        fr = DistSSHRun._ride_rewrite(fill)
        @test fr.head === :call
        @test fr.args[1] === GlobalRef(DistSSHRun, :_ride_index_fill!)
        @test !(
            fr.args[4] isa Expr && fr.args[4].head === :call &&
                fr.args[4].args[1] === :collect
        )
        ys = zeros(Int, 3)
        @test DistSSHRun._ride_index_fill!(ys, i -> i * i, 1:3) === nothing
        @test ys == [1, 4, 9]

        acc = Meta.parse("for x in xs; s += x; end")
        @test DistSSHRun._ride_rewrite(acc).head === :for

        stencil = Meta.parse("for i in 2:length(dest); dest[i] = alias[i - 1]; end")
        @test DistSSHRun._ride_rewrite(stencil).head === :for

        pre = DistSSHRun._ride_worker_prelude(
            Meta.parseall(
                """
                function work(x)
                    x + 1
                end
                ys = map(work, 1:3)
                """
            )
        )
        s = string(pre)
        @test occursin("work", s)
        @test !occursin("map", s)

        plan = DistSSHRun._ride_resolve_plan(["child:h:2", "parent:1"])
        @test plan.parent_workers == 1
        @test plan.child_workers["h"] == 2

        _with_tempdir() do tmp
            script = joinpath(tmp, "map.jl")
            d1 = DistSSHRun._ride_batch_dir(script, nothing; project = tmp)
            d2 = DistSSHRun._ride_batch_dir(script, nothing; project = tmp)
            @test isdir(d1) && isdir(d2)
            @test d1 != d2
        end
    end

    @testset "kit_run_result" begin
        r = DistSSHRun.RideResult(true, "s.jl", 2, true, nothing, "1.13")
        kr = DistSSHRun.kit_run_result(r)
        @test kr.ok
        @test kr.kind === :ride
        @test kr.exit_code == 0
        @test kr.failed_step === nothing
        bad = DistSSHRun.RideResult(false, "s.jl", 0, nothing, "boom", "1.13")
        @test DistSSHRun.kit_run_result(bad).failed_step == "ride"
    end

    _with_tempdir() do tmp
        out = joinpath(tmp, "out.txt")
        map_path = joinpath(tmp, "mapped.jl")
        write(
            map_path, """
            ys = map(x -> x + 1, 1:4)
            write($(repr(out)), join(string.(ys), ","))
            """
        )
        r = DistSSHRun.ride!(map_path, "parent:1"; spi_check = true)
        @test r.ok
        @test r.workers == 1
        @test r.spi_ok === true
        @test read(out, String) == "2,3,4,5"
        @test r.output_dir !== nothing
        @test !isfile(joinpath(something(r.output_dir, ""), "kit.hosts"))
        st = DistSSHRun.drive_host_status(something(r.output_dir, ""))
        @test any(row -> row.host == DistSSHRun.PARENT_HOST_NAME, st)

        _with_tempdir() do hosts_tmp
            DistSSHRun._write_kit_hosts_file(["alice@h1", "bob@h2"], hosts_tmp, nothing)
            @test DistSSHRun._read_kit_hosts(hosts_tmp) == ["alice@h1", "bob@h2"]
        end

        fout = joinpath(tmp, "filt.txt")
        filt = joinpath(tmp, "filt.jl")
        write(
            filt, """
            ys = filter(iseven, 1:6)
            write($(repr(fout)), join(string.(ys), ","))
            """
        )
        rf = DistSSHRun.ride!(filt, "parent:1"; spi_check = true)
        @test rf.ok
        @test read(fout, String) == "2,4,6"

        named = joinpath(tmp, "named.jl")
        nout = joinpath(tmp, "named.txt")
        write(
            named, """
            function work(x)
                x * x
            end
            ys = map(work, 1:3)
            write($(repr(nout)), join(string.(ys), ","))
            """
        )
        rn = DistSSHRun.ride!(named, "parent:1"; spi_check = true)
        @test rn.ok
        @test read(nout, String) == "1,4,9"

        loop = joinpath(tmp, "loop.jl")
        lout = joinpath(tmp, "loop.txt")
        write(
            loop, """
            xs = 1:4
            ys = similar(collect(xs))
            for i in eachindex(xs)
                ys[i] = xs[i] * xs[i]
            end
            write($(repr(lout)), join(string.(ys), ","))
            """
        )
        rl = DistSSHRun.ride!(loop, "parent:1"; spi_check = true)
        @test rl.ok
        @test rl.spi_ok !== false
        @test read(lout, String) == "1,4,9,16"

        val_path = joinpath(tmp, "forval.jl")
        vout = joinpath(tmp, "forval.txt")
        write(
            val_path, """
            ys = zeros(Int, 3)
            x = (for i in eachindex(ys)
                ys[i] = i * i
            end)
            write($(repr(vout)), string(x === nothing) * ";" * join(string.(ys), ","))
            """
        )
        rv = DistSSHRun.ride!(val_path, "parent:1"; spi_check = true)
        @test rv.ok
        @test read(vout, String) == "true;1,4,9"

        st_path = joinpath(tmp, "stateful.jl")
        stout = joinpath(tmp, "stateful.txt")
        write(
            st_path, """
            xs = [1, 2, 3, 4]
            ys = similar(xs)
            for i in Iterators.Stateful(eachindex(xs))
                ys[i] = xs[i] * xs[i]
            end
            write($(repr(stout)), join(string.(ys), ","))
            """
        )
        rs = DistSSHRun.ride!(st_path, "parent:1"; spi_check = false)
        @test rs.ok
        @test read(stout, String) == "1,4,9,16"

        alias_path = joinpath(tmp, "alias.jl")
        aout = joinpath(tmp, "alias.txt")
        write(
            alias_path, """
            dest = [1, 2, 3]
            alias = dest
            for i in 2:length(dest)
                dest[i] = alias[i - 1]
            end
            write($(repr(aout)), join(string.(dest), ","))
            """
        )
        ra = DistSSHRun.ride!(alias_path, "parent:1"; spi_check = false)
        @test ra.ok
        @test read(aout, String) == "1,1,1"

        obs_path = joinpath(tmp, "observe.jl")
        oout = joinpath(tmp, "observe.txt")
        write(
            obs_path, """
            dest = [1, 2, 3]
            seen = Int[]
            observe(i) = (push!(seen, dest[1]); 0)
            for i in eachindex(dest)
                dest[i] = observe(i)
            end
            write($(repr(oout)), join(string.(seen), ",") * ";" * join(string.(dest), ","))
            """
        )
        ro = DistSSHRun.ride!(obs_path, "parent:1"; spi_check = false)
        @test ro.ok
        @test read(oout, String) == "1,0,0;0,0,0"

        gen_path = joinpath(tmp, "geniter.jl")
        gout = joinpath(tmp, "geniter.txt")
        write(
            gen_path, """
            dest = zeros(Int, 3)
            n = Ref(0)
            struct _RideUnknownIter
                n::Ref{Int}
            end
            Base.IteratorSize(::Type{_RideUnknownIter}) = Base.SizeUnknown()
            function Base.iterate(it::_RideUnknownIter, st=1)
                st > 3 && return nothing
                it.n[] += 1
                return (st, st + 1)
            end
            for i in _RideUnknownIter(n)
                dest[i] = n[]
            end
            write($(repr(gout)), join(string.(dest), ","))
            """
        )
        rg = DistSSHRun.ride!(gen_path, "parent:1"; spi_check = false)
        @test rg.ok
        @test read(gout, String) == "1,2,3"

        haslen_path = joinpath(tmp, "hasleniter.jl")
        hout = joinpath(tmp, "hasleniter.txt")
        write(
            haslen_path, """
            dest = zeros(Int, 3)
            n = Ref(0)
            struct _RideHasLenIter
                n::Ref{Int}
            end
            Base.IteratorSize(::Type{_RideHasLenIter}) = Base.HasLength()
            Base.length(::_RideHasLenIter) = 3
            function Base.iterate(it::_RideHasLenIter, st=1)
                st > 3 && return nothing
                it.n[] += 1
                return (st, st + 1)
            end
            for i in _RideHasLenIter(n)
                dest[i] = n[]
            end
            write($(repr(hout)), join(string.(dest), ","))
            """
        )
        rh = DistSSHRun.ride!(haslen_path, "parent:1"; spi_check = false)
        @test rh.ok
        @test read(hout, String) == "1,2,3"

        pmapnest_path = joinpath(tmp, "pmapnest.jl")
        pout = joinpath(tmp, "pmapnest.txt")
        write(
            pmapnest_path, """
            function inner_overlap()
                data = [1, 2, 3, 4]
                dest = @view data[2:4]
                src = @view data[1:3]
                for i in eachindex(dest)
                    dest[i] = src[i]
                end
                return join(string.(dest), ",")
            end
            out = Vector{String}(undef, 4)
            for i in eachindex(out)
                out[i] = inner_overlap()
            end
            write($(repr(pout)), join(out, ";"))
            """
        )
        rp = DistSSHRun.ride!(pmapnest_path, "parent:2"; spi_check = false)
        @test rp.ok
        @test read(pout, String) == "1,1,1;1,1,1;1,1,1;1,1,1"

        overlap_src = """
        data = [1, 2, 3, 4]
        dest = @view data[2:4]
        src = @view data[1:3]
        for i in eachindex(dest)
            dest[i] = src[i]
        end
        write(ARGS[1], join(string.(dest), ","))
        """
        holder_src(struct_name) = """
        struct $(struct_name)
            src::AbstractArray
        end
        data = [1, 2, 3, 4]
        dest = @view data[2:4]
        holder = $(struct_name)(@view data[1:3])
        for i in eachindex(dest)
            dest[i] = holder.src[i]
        end
        write(ARGS[1], join(string.(dest), ","))
        """
        nest_n = DistSSHRun._RIDE_CAPTURE_DEPTH + 3
        nested_src(struct_name) = begin
            nest_wraps = join(
                ["w = $(struct_name)(w)" for _ in 1:(nest_n - 1)],
                "\n            ",
            )
            nest_get = "w" * repeat(".inner", nest_n)
            """
            struct $(struct_name)
                inner
            end
            data = [1, 2, 3, 4]
            dest = @view data[2:4]
            w = $(struct_name)(@view data[1:3])
            $(nest_wraps)
            for i in eachindex(dest)
                dest[i] = $(nest_get)[i]
            end
            write(ARGS[1], join(string.(dest), ","))
            """
        end
        const_nospi_src = """
        const _RideConstData_nospi = [1, 2, 3, 4]
        dest = @view _RideConstData_nospi[2:4]
        const _RideConstSrc_nospi = @view _RideConstData_nospi[1:3]
        for i in eachindex(dest)
            dest[i] = Main._RideConstSrc_nospi[i]
        end
        write(ARGS[1], join(string.(dest), ","))
        """
        const_spi_src = """
        const _RideConstData_spi = [1, 2, 3, 4]
        dest = @view _RideConstData_spi[2:4]
        const _RideConstSrc_spi = @view _RideConstData_spi[1:3]
        for i in eachindex(dest)
            dest[i] = Main._RideConstSrc_spi[i]
        end
        write(ARGS[1], join(string.(dest), ","))
        """
        for (body, spi, stem) in (
                (overlap_src, false, "overlap_nospi"),
                (overlap_src, true, "overlap_spi"),
                (holder_src("_RideViewHolder_nospi"), false, "holder_nospi"),
                (holder_src("_RideViewHolder_spi"), true, "holder_spi"),
                (nested_src("_RideNest_nospi"), false, "nested_nospi"),
                (nested_src("_RideNest_spi"), true, "nested_spi"),
                (const_nospi_src, false, "const_nospi"),
                (const_spi_src, true, "const_spi"),
            )
            opath = joinpath(tmp, stem * ".jl")
            oout = joinpath(tmp, stem * ".txt")
            write(opath, replace(body, "ARGS[1]" => repr(oout)))
            rr = DistSSHRun.ride!(opath, "parent:1"; spi_check = spi)
            @test rr.ok
            @test read(oout, String) == "1,1,1"
        end

        child = DistSSHRun.ride!(map_path, "child:host1")
        @test !child.ok
        @test occursin(":N", something(child.error, ""))

        drive_path = joinpath(tmp, "driver.jl")
        write(drive_path, "using Distributed\npmap(x -> x, 1:2)\n")
        rd = DistSSHRun.ride!(drive_path)
        @test !rd.ok
        @test occursin("drive", something(rd.error, ""))
        @test occursin("pmap", something(rd.error, ""))

        buf = IOBuffer()
        DistSSHRun.print_ride(r; io = buf)
        shown = String(take!(buf))
        @test startswith(shown, "DistSSHKit ride\n")
        @test occursin("Script: ", shown)
        @test occursin("Workers: ", shown)
        @test occursin("Julia: ", shown)
        @test occursin("DistSSHRun: ", shown)
        @test occursin("SPI check: passed", shown)
        @test !occursin("Ride:", shown)
    end
end
