using Test

# Module setup rsync / clone dest messaging (CLI exit codes in integration/setup/exit.jl).

@testset "setup rsync" begin
    remote_path = "~/App.jl"
    project = _kit_root()

    function _with_fake_remotes(f::Function; extra_env = Dict{String, String}())
        _with_tempdir() do state_dir
            withenv(merge(_fake_setup_remote_env(state_dir), extra_env)...) do
                _apply_quiet_setup_session!()
                return f(state_dir)
            end
        end
    end

    function _mark_nonempty!(state_dir::AbstractString, host::AbstractString)
        slot = replace(host, r"[@:/]" => "_")
        tree = joinpath(state_dir, slot, "tree")
        mkpath(tree)
        touch(joinpath(tree, "keepme.txt"))
    end

    @testset "rsync -e quotes ssh" begin
        Sys.which("ssh") === nothing && return
        withenv("DISTSSHKIT_TEST_SSH" => nothing) do
            t = DistSSHRun._host_sync_rsync_transport()
            @test startswith(t, Base.shell_escape(DistSSHRun._ssh_exe()))
            cmd = DistSSHRun._host_sync_remote_shell_cmd("host.test", "true")
            @test cmd.exec[1] == DistSSHRun._ssh_exe()
            @test "-n" in cmd.exec
        end
    end

    @testset "remote_dir / ensure" begin
        _with_fake_remotes() do _
            host = "user@host.test"
            @test DistSSHRun.remote_dir_exists(host, remote_path) == false
            @test DistSSHRun.ensure_remote_dir(host, remote_path) == true
            @test DistSSHRun.remote_dest_status(host, remote_path) === :empty
        end
        _with_fake_remotes(extra_env = Dict("DISTSSHKIT_TEST_MKDIR_FAIL" => "1")) do _
            @test DistSSHRun.ensure_remote_dir("user@host.test", remote_path) == false
        end
    end

    @testset "rsync outcomes" begin
        # A user tree. The package checkout path-depends on DistSSHBase, which
        # setup correctly refuses to ship.
        project = mktempdir()
        write(joinpath(project, "Project.toml"), "name = \"RsyncSmoke\"\n")
        write(joinpath(project, "smoke.jl"), "smoke = 1\n")
        withenv("DISTSSHKIT_YES" => nothing) do
            prev_ni = DistSSHRun.kit_noninteractive()
            DistSSHRun.set_kit_noninteractive!(false)
            try
                for v in (:quiet, :progress, :verbose)
                    with_kit_verbosity(v) do
                        _with_active_kit_progress() do _
                            out, result, nsus = _capture_stdio(; probe_suspend = true) do stdin_io, _
                                println(stdin_io, "")
                                flush(stdin_io)
                                seekstart(stdin_io)
                                DistSSHRun.rsync_push_to_remotes(["host1"], remote_path, project)
                            end
                            @test result == (cancelled = true, succeeded = 0, failed = 0)
                            @test occursin("Cancelled.", out)
                            @test occursin("bypasses git", out)
                            @test occursin("Type 'rsync'", out)
                            # #374: bar is suspended while stdin is read, then released.
                            @test nsus >= 1
                            @test DistSSHRun.KIT_PROGRESS_SUSPEND[] == 0
                        end
                    end
                end
            finally
                DistSSHRun.set_kit_noninteractive!(prev_ni)
            end
        end

        _with_fake_remotes(extra_env = Dict("DISTSSHKIT_TEST_MKDIR_FAIL" => "1")) do _
            raw = DistSSHRun.rsync_project_to_hosts!(
                ["host1"], project, remote_path; confirm = false, report = false,
            )
            @test raw.succeeded == 0 && raw.failed == 1
        end

        _with_fake_remotes() do _
            raw = DistSSHRun.rsync_project_to_hosts!(
                ["host1"], project, remote_path; confirm = false, report = false,
            )
            @test raw.succeeded == 1 && raw.failed == 0
        end

        _with_fake_remotes() do _
            withenv("DISTSSHKIT_JOBS" => "2") do
                raw = DistSSHRun.rsync_project_to_hosts!(
                    ["host1", "host2"], project, remote_path; confirm = false, report = false,
                )
                @test raw.succeeded == 2 && raw.failed == 0
                @test [hr.host for hr in raw.host_results] == ["host1", "host2"]
            end
        end

        _with_fake_remotes() do state_dir
            host = "user@host.test"
            _mark_nonempty!(state_dir, host)
            DistSSHRun.apply_kit_cli_session!(DistSSHRun.KitCliSession(quiet = false, yes = true))
            out, result = _capture_stdio() do _, _
                DistSSHRun.rsync_push_to_remotes([host], remote_path, project)
            end
            @test result == (cancelled = false, succeeded = 0, failed = 1)
            @test occursin("refusing to overwrite", out)
        end

        _with_fake_remotes(extra_env = Dict("DISTSSHKIT_TEST_RSYNC_FAIL" => "1")) do _
            raw = DistSSHRun.rsync_project_to_hosts!(
                ["host1"], project, remote_path; confirm = false, report = false,
            )
            @test raw.succeeded == 0 && raw.failed == 1
        end
    end
end

@testset "setup clone dest safety" begin
    remote_path = "~/App.jl"

    _with_tempdir() do state_dir
        withenv(_fake_setup_remote_env(state_dir)...) do
            DistSSHRun.apply_kit_cli_session!(DistSSHRun.KitCliSession(quiet = false, yes = true))
            host = "host1"
            slot = replace(host, r"[@:/]" => "_")
            tree = joinpath(state_dir, slot, "tree")
            mkpath(tree)
            touch(joinpath(tree, "keepme.txt"))
            out, result = _capture_stdio() do _, _
                DistSSHRun.clone_to_remotes(
                    [host], remote_path, "git@example.com:org/App.jl.git",
                )
            end
            @test !result.cancelled && result.succeeded == 0 && result.failed == 1
            @test length(result.hosts) == 1 && !result.hosts[1].ok
            @test occursin("refusing to overwrite", out)
            @test isfile(joinpath(tree, "keepme.txt"))
        end
    end
end
