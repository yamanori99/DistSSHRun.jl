using Test

@testset "remote" begin
    @test DistSSHRun.ssh_addprocs_machine("dev@host1") == "dev@host1"
    @test DistSSHRun.ssh_addprocs_machine("  alice@h  ") == "alice@h"

    @test DistSSHRun.normalize_git_clone_url("https://github.com/org/App.jl.git") ==
        "git@github.com:org/App.jl.git"
    @test DistSSHRun.normalize_git_clone_url("git@github.com:org/App.jl.git") ==
        "git@github.com:org/App.jl.git"

    @test DistSSHRun.default_remote_project_path("/Users/z/GitHub/MyApp.jl") ==
        joinpath("~", "GitHub", "MyApp.jl")

    withenv("DISTRIBUTED_REMOTE_PROJECT_ROOT" => nothing) do
        @test DistSSHRun.resolve_remote_project_root("/Users/z/GitHub/MyApp.jl") ==
            joinpath("~", "GitHub", "MyApp.jl")
    end
    withenv("DISTRIBUTED_REMOTE_PROJECT_ROOT" => "/Volumes/shared/MyApp.jl") do
        @test DistSSHRun.resolve_remote_project_root("/Users/z/GitHub/MyApp.jl") ==
            "/Volumes/shared/MyApp.jl"
    end
    @test DistSSHRun.resolve_remote_project_root(
        "/Users/z/GitHub/MyApp.jl";
        cli_override = "~/work/MyApp.jl",
    ) == "~/work/MyApp.jl"
    @test DistSSHRun.remote_env_project_root("~/jobs/abc") == "~/jobs/abc"
    @test DistSSHRun.remote_env_project_root("~/.distsshkitqueue/jobs/x") ==
        "~/.distsshkitqueue/jobs/x"
    @test DistSSHRun.remote_env_project_root("/remote/App.jl") ==
        DistSSHRun.canonical_local_path("/remote/App.jl")
    withenv("DISTRIBUTED_REMOTE_PROJECT_ROOT" => "/Volumes/shared/MyApp.jl") do
        @test DistSSHRun.resolve_remote_project_root(
            "/Users/z/GitHub/MyApp.jl";
            cli_override = "~/work/MyApp.jl",
        ) == "~/work/MyApp.jl"
    end

    @test DistSSHRun.local_dir_from_remote_mirror(
        "/Volumes/r/MyRepo/data/sweep/slug/20260101_120000",
        "/Volumes/r/MyRepo",
        "/Users/z/MyRepo",
    ) == joinpath("/Users/z/MyRepo", "data", "sweep", "slug", "20260101_120000") |> abspath
    @test_throws ArgumentError DistSSHRun.local_dir_from_remote_mirror(
        "~/r/MyRepo/data",
        "~/r/MyRepo",
        "/Users/z/MyRepo",
    )
    @test_throws ArgumentError DistSSHRun.local_dir_from_remote_mirror(
        "/Volumes/r/MyRepo/data",
        "~/r/MyRepo",
        "/Users/z/MyRepo",
    )

    @test DistSSHRun.resolve_remote_abs_path_on_host("host", "/data/MyRepo") == "/data/MyRepo"
    let missing = DistSSHRun._remote_abs_path_resolve_shell("~/distsshkit-e2e-tilde/output")
        @test occursin("printf", missing)
        @test !occursin("else exit 1; fi", missing)
    end
    let abs = DistSSHRun._remote_abs_path_resolve_shell("/data/MyRepo/output")
        @test occursin("else exit 1; fi", abs)
        @test !occursin("printf", abs)
    end
    let spaced = "~/Repo With Spaces/output"
        word = DistSSHRun._remote_shell_path_word(spaced)
        @test startswith(word, "~/")
        @test word != Base.shell_escape(spaced)
        @test occursin(Base.shell_escape("Repo With Spaces/output"), word)
        sh = DistSSHRun._remote_abs_path_resolve_shell(spaced)
        @test occursin("printf", sh)
        @test occursin(word, sh)
    end

    @test DistSSHRun.remote_path_for_ssh_collect(
        "/Users/z/MyRepo/data/out",
        "/Users/z/MyRepo",
    ) == joinpath("~", "z", "MyRepo", "data", "out")
    withenv("DISTRIBUTED_REMOTE_PROJECT_ROOT" => "/Volumes/z/clone/MyRepo") do
        @test DistSSHRun.remote_path_for_ssh_collect(
            "/Users/z/MyRepo/data/sweep/x/ts",
            "/Users/z/MyRepo",
        ) == joinpath("/Volumes/z/clone/MyRepo", "data", "sweep", "x", "ts") |> abspath
    end
    withenv("DISTRIBUTED_REMOTE_PROJECT_ROOT" => "~/work/MyRepo") do
        @test DistSSHRun.remote_path_for_ssh_collect(
            "/Users/z/MyRepo/demos/with_kit",
            "/Users/z/MyRepo",
        ) == joinpath("~/work/MyRepo", "demos", "with_kit")
    end

    @testset "ensure_remote_abs_path" begin
        @test DistSSHRun.ensure_remote_abs_path("host", "/home/dev/App") == "/home/dev/App"
        @test DistSSHRun.ensure_remote_abs_path("host", "") === nothing
        @test DistSSHRun.ensure_remote_abs_path("host", "   ") === nothing
    end

    @testset "resolve_host_path_abs" begin
        _with_tempdir() do tmp
            p = DistSSHRun.canonical_local_path(tmp)
            @test DistSSHRun.resolve_host_project_abs("parent", p) == p
            withenv("DISTRIBUTED_REMOTE_PROJECT_ROOT" => "/Volumes/z/clone/MyRepo") do
                @test DistSSHRun.resolve_host_project_abs("host", p) == "/Volumes/z/clone/MyRepo"
            end
        end
    end

    withenv("DISTRIBUTED_SSH_OPTS" => nothing) do
        opts = DistSSHRun.build_ssh_opts()
        @test "-o" in opts
        @test "BatchMode=yes" in opts
        @test "RequestTTY=no" in opts
        @test DistSSHRun.ssh_opts() == String.(opts)
        tty = DistSSHRun.ssh_opts(; request_tty = true)
        @test !("RequestTTY=no" in tty)
        @test "BatchMode=yes" in tty
        @test DistSSHRun.build_ssh_opts(; request_tty = true) == tty
        @test DistSSHRun.ssh_opts(; request_tty = false) == DistSSHRun.ssh_opts()
    end
    withenv("DISTRIBUTED_SSH_OPTS" => "-o Foo=bar -o Baz=qux") do
        @test DistSSHRun.build_ssh_opts() == ["-o", "Foo=bar", "-o", "Baz=qux"]
        @test DistSSHRun.ssh_opts() == ["-o", "Foo=bar", "-o", "Baz=qux"]
        @test DistSSHRun.ssh_opts(; request_tty = true) == ["-o", "Foo=bar", "-o", "Baz=qux"]
    end
    withenv("DISTRIBUTED_SSH_OPTS" => "-F /tmp/ssh_config") do
        @test DistSSHRun.ssh_opts() == ["-F", "/tmp/ssh_config"]
        @test DistSSHRun.ssh_opts(; request_tty = true) == ["-F", "/tmp/ssh_config"]
    end

    let r = DistSSHRun.get_local_resources()
        @test r.total_gb > 0
        @test r.nproc >= 1
    end

    _with_tempdir() do tmp
        d = tmp
        @test DistSSHRun.get_local_git_hash(d) === nothing
        @test DistSSHRun.clone_url_from_local_origin(d) === nothing
        @test DistSSHRun.local_git_clean(d)

        Sys.which("git") === nothing && return
        run(Cmd(["git", "-C", d, "init", "-q"]))
        run(Cmd(["git", "-C", d, "config", "user.email", "test@example.com"]))
        run(Cmd(["git", "-C", d, "config", "user.name", "Test"]))
        @test DistSSHRun.local_git_clean(d)

        write(joinpath(d, "f.txt"), "hi")
        @test !DistSSHRun.local_git_clean(d)

        run(Cmd(["git", "-C", d, "add", "f.txt"]))
        run(Cmd(["git", "-C", d, "commit", "-q", "-m", "init"]))
        @test DistSSHRun.local_git_clean(d)

        write(joinpath(d, "f.txt"), "hi2")
        @test !DistSSHRun.local_git_clean(d)

        run(Cmd(["git", "-C", d, "add", "f.txt"]))
        run(Cmd(["git", "-C", d, "commit", "-q", "-m", "dirty"]))
        @test DistSSHRun.local_git_clean(d)
        idx = joinpath(d, ".git", "index")
        if isfile(idx)
            backup = read(idx)
            write(idx, "not-a-git-index")
            try
                @test DistSSHRun.get_local_git_hash(d) isa String
                @test !DistSSHRun.local_git_clean(d)
            finally
                write(idx, backup)
            end
        end

        full = DistSSHRun.get_local_git_hash(d)
        @test full isa String
        full isa String || error("expected full git hash")
        @test length(full) == 40
        short = DistSSHRun.get_local_git_hash(d; short = 8)
        @test short isa String
        short isa String || error("expected short git hash")
        @test length(short) == 8
        @test startswith(full, short)

        run(Cmd(["git", "-C", d, "remote", "add", "origin", "https://github.com/org/App.jl.git"]))
        @test DistSSHRun.clone_url_from_local_origin(d) == "git@github.com:org/App.jl.git"
    end

    @test DistSSHRun.parse_julia_version("julia version 1.13.1") == v"1.13.1"
    @test DistSSHRun.parse_julia_version("julia version 1.9.0-DEV") == v"1.9.0"
    @test DistSSHRun.parse_julia_version("julia version 1.13.0-beta1") == v"1.13.0"
    @test DistSSHRun.parse_julia_version("") === nothing
    @test DistSSHRun.parse_julia_version("not julia at all") === nothing

    @testset "remote_julia_candidates" begin
        darwin = DistSSHRun.remote_julia_candidates("Darwin")
        @test darwin[1] == raw"$HOME/.juliaup/bin/julia"
        @test "/opt/homebrew/bin/julia" in darwin
        @test "/usr/local/bin/julia" in darwin
        linux = DistSSHRun.remote_julia_candidates("Linux")
        @test linux[1] == raw"$HOME/.juliaup/bin/julia"
        @test "/usr/bin/julia" in linux
        @test !("/opt/homebrew/bin/julia" in linux)
    end

    @testset "resolve_controller_julia" begin
        p = DistSSHRun.resolve_controller_julia("auto")
        @test isabspath(p)
        @test isfile(p)
        @test DistSSHRun.resolve_controller_julia(nothing) == p
        @test DistSSHRun.resolve_controller_julia(p) == p
        @test_throws ArgumentError DistSSHRun.resolve_controller_julia("/no/such/julia")
    end

    @testset "_remote_shell_path_word" begin
        @test DistSSHRun._remote_shell_path_word("~/proj") == "~/proj"
        @test DistSSHRun._remote_shell_path_word("~/Repo With Spaces/output") ==
            "~/" * Base.shell_escape("Repo With Spaces/output")
        spaced = "/opt/Julia 1.12/bin/julia"
        @test DistSSHRun._remote_shell_path_word(spaced) == Base.shell_escape(spaced)
        meta = "/tmp/j;rm -rf /"
        @test DistSSHRun._remote_shell_path_word(meta) == Base.shell_escape(meta)
        @test DistSSHRun._remote_shell_path_word(meta) != meta
    end

    # Compat: ordinary remote roots keep the same shell text after quoting via the helper.
    @testset "compat remote git shells" begin
        tilde = "~/App.jl"
        abs = "/opt/App.jl"
        pq_abs = DistSSHRun._remote_shell_path_word(abs)
        @test DistSSHRun._git_pull_remote_inner(tilde) == "cd ~/App.jl && git pull"
        @test DistSSHRun._git_pull_remote_inner(abs) == "cd $pq_abs && git pull"
        @test DistSSHRun._remote_git_hash_inner(tilde) == "cd ~/App.jl && git rev-parse HEAD"
        @test DistSSHRun._remote_git_hash_inner(abs) == "git -C $pq_abs rev-parse HEAD"
        @test DistSSHRun._remote_git_hash_inner(abs; short = 8) == "git -C $pq_abs rev-parse --short=8 HEAD"
        @test pq_abs == abs
    end

    @testset "detect_julia_path cache" begin
        empty!(DistSSHRun._DETECT_JULIA_PATH_CACHE)
        try
            @test DistSSHRun.detect_julia_path("") === nothing
            @test !haskey(DistSSHRun._DETECT_JULIA_PATH_CACHE, "")
            @test DistSSHRun.detect_julia_path("no-such-host.invalid") === nothing
            @test DistSSHRun._DETECT_JULIA_PATH_CACHE["no-such-host.invalid"] === nothing
            DistSSHRun._DETECT_JULIA_PATH_CACHE["cache-hit.host"] = "/opt/julia"
            @test DistSSHRun.detect_julia_path("cache-hit.host") == "/opt/julia"
            DistSSHRun.clear_detect_julia_path_cache!("cache-hit.host")
            @test !haskey(DistSSHRun._DETECT_JULIA_PATH_CACHE, "cache-hit.host")
            DistSSHRun._DETECT_JULIA_PATH_CACHE["a"] = "/a"
            DistSSHRun._DETECT_JULIA_PATH_CACHE["b"] = "/b"
            DistSSHRun.clear_detect_julia_path_cache!()
            @test isempty(DistSSHRun._DETECT_JULIA_PATH_CACHE)
        finally
            empty!(DistSSHRun._DETECT_JULIA_PATH_CACHE)
        end
    end

    @testset "detect_julia_path skips Linux candidates when uname fails" begin
        _with_tempdir() do state_dir
            logp = joinpath(state_dir, "ssh.log")
            env = merge(
                _fake_setup_remote_env(state_dir),
                Dict(
                    "DISTSSHKIT_TEST_UNAME_FAIL" => "1",
                    "DISTSSHKIT_TEST_JULIA_WHICH" => "/opt/custom/julia",
                    "DISTSSHKIT_TEST_SSH_LOG" => logp,
                ),
            )
            empty!(DistSSHRun._DETECT_JULIA_PATH_CACHE)
            try
                withenv(env...) do
                    @test DistSSHRun.detect_julia_path("host1") == "/opt/custom/julia"
                end
                body = isfile(logp) ? read(logp, String) : ""
                @test occursin("uname -s", body)
                @test occursin("command -v julia", body)
                @test !occursin("/usr/bin/julia", body)
            finally
                empty!(DistSSHRun._DETECT_JULIA_PATH_CACHE)
            end
        end
    end

    @test DistSSHRun.get_remote_julia_version("no-such-host.invalid", "/usr/bin/julia") === nothing
    @test DistSSHRun.detect_julia_path("no-such-host.invalid") === nothing
    @test DistSSHRun.resolve_remote_julia("no-such-host.invalid", "auto") === nothing

    @testset "run_on_host remote sh" begin
        sh = DistSSHRun._run_on_host_remote_sh(["-e", "1"]; detect = true)
        @test occursin("uname -s", sh)
        @test occursin("exec", sh)
        @test occursin(raw"$HOME/.juliaup/bin/julia", sh)
        @test occursin("/opt/homebrew/bin/julia", sh)
        @test occursin("-e", sh)
        @test DistSSHRun._remote_argv_sh(["-e", "exit(3)"]) == "'-e' 'exit(3)'"
        @test DistSSHRun._remote_sh_quote("a'b") == raw"'a'\''b'"
        expl = DistSSHRun._run_on_host_remote_sh(["--version"]; julia = "/opt/julia", detect = false)
        @test occursin("/opt/julia", expl)
        @test occursin("exec", expl)
        @test !occursin("uname", expl)
        @test_throws ArgumentError DistSSHRun._run_on_host_remote_sh(
            String[]; detect = false, julia = nothing,
        )
        @test_throws ArgumentError DistSSHRun.run_on_host("", ["--version"])
        withenv("PATH" => "/nonexistent-distsshkit-path") do
            @test occursin(
                "ssh not found in PATH",
                try
                    DistSSHRun.run_on_host("no-such-host.invalid", ["--version"])
                    ""
                catch e
                    @test e isa ArgumentError
                    sprint(showerror, e)
                end,
            )
            @test occursin(
                "OpenSSH", sprint(
                    showerror, try
                        DistSSHRun._host_tool_exe("ssh")
                        error("expected")
                    catch e
                        e
                    end
                )
            )
            @test occursin(
                "rsync not found", sprint(
                    showerror, try
                        DistSSHRun._host_tool_exe("rsync")
                        error("expected")
                    catch e
                        e
                    end
                )
            )
            @test occursin(
                "git not found", sprint(
                    showerror, try
                        DistSSHRun._host_tool_exe("git")
                        error("expected")
                    catch e
                        e
                    end
                )
            )
            @test_throws ArgumentError DistSSHRun._remote_ssh_ok("no-such-host.invalid")
            @test_throws ArgumentError DistSSHRun.git_pull_remote_host!("x", ".")
            @test occursin(
                "scp not found", sprint(
                    showerror, try
                        DistSSHRun._host_tool_exe("scp")
                        error("expected")
                    catch e
                        e
                    end
                )
            )
        end
        if Sys.which("ssh") !== nothing
            let p = redirect_stderr(devnull) do
                    DistSSHRun.run_on_host("no-such-host.invalid", ["--version"])
                end
                @test p isa Base.Process
                @test p.exitcode != 0
            end
            @test !DistSSHRun._remote_ssh_ok("no-such-host.invalid")
        end
    end
end
