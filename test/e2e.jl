#!/usr/bin/env julia
# Real-SSH E2E against testenv/docker-ssh workers. Not part of Pkg.test().
# Oracle: OpenSSH + rsync + remote Julia (setup / drive / go / ride / git). Assert
# files workers write (`run_on_host` / collect). Kit-parent-side demo CSV is not
# a remote collect. Inventory: test/README.md § SSH E2E.
# Local with_kit recipes are test/integration/demos/.
#
#   testenv/docker-ssh/scripts/up.sh --e2e
#   testenv/apple-container-ssh/scripts/up.sh --e2e   # Mac Apple silicon; same suite
#   DISTSSHKIT_SSH_E2E=1 julia --project=. test/e2e.jl   # from kit root
# Inner `@testset`s print `[i/N]` (see `_E2E_N`) so CI logs are not a long stall.
#   DISTSSHKIT_CODE_COVERAGE=1 …/up.sh --e2e            # + .cov (child CLI too)
#   ./.github/jetls-check.sh             # e2e.jl is a JETLS entry (not via runtests.jl)
#
# Afterward open only:
#   $(cat test/artifacts/ssh-e2e/LATEST)/SUMMARY.txt

using Test
using Distributed
using DistSSHRun

# Same include shape as `test/runtests.jl` so JETLS follows it. Do not route
# through a non-`const` `kit_root` (JETLS then skips the include).
include(joinpath(@__DIR__, "support.jl"))
DistSSHRun.set_kit_verbosity!(:progress)

if !_ssh_e2e_enabled()
    @info "Skipping SSH E2E (set DISTSSHKIT_SSH_E2E=1 to enable)"
    exit(0)
end

const g = _docker_ssh_generated()
if !isfile(g.ssh_config) || !isfile(g.hosts_file)
    error("docker-ssh not ready: missing $(g.ssh_config). Run testenv/docker-ssh/scripts/up.sh")
end

const hosts = collect(String, _ssh_e2e_hosts())
const setup_hosts = String["child:$h" for h in hosts]
const remote_root = _ssh_e2e_remote_root()
const remote_tokens = String["child:$(hosts[1]):1", "child:$(hosts[2]):1"]
# ENV overlay for the suite (`-F` ssh config and the remote project path).
_e2e_base_env() = _ssh_e2e_env(; remote_project = remote_root)

# Same banner idea as `test/runtests.jl`. Inner `@testset`s can take minutes
# of SSH with no Test output until they finish. Update `_E2E_N` when adding one.
const _E2E_N = 29
const _E2E_I = Ref(0)
# Print `[i/N]` before an inner `@testset`.
function _e2e_announce(label::AbstractString)
    _E2E_I[] += 1
    println("[$(_E2E_I[])/$_E2E_N]  $label")
    flush(stdout)
    return nothing
end

@testset "SSH E2E (docker-ssh)" verbose = true begin
    _with_ssh_e2e_suite() do suite
        @testset "julia path resolve (kit parent + remotes)" begin
            _e2e_announce("julia path resolve (kit parent + remotes)")
            withenv(_e2e_base_env()...) do
                ctrl = DistSSHRun.resolve_controller_julia("auto")
                @test isabspath(ctrl)
                @test isfile(ctrl)
                @test ctrl != "julia"
                ctrl_ver = DistSSHRun.parse_julia_version(read(`$ctrl --version`, String))
                @test ctrl_ver isa VersionNumber
                os_label = Sys.isapple() ? "darwin" : (Sys.islinux() ? "linux" : Sys.KERNEL)
                _ssh_e2e_record_julia!(suite, "kit_parent($(os_label))", ctrl, string(ctrl_ver))
                _assert_ssh_e2e_api_ok(suite, "kit_parent_julia", true, "path=$(ctrl) ver=$(ctrl_ver)")

                for host in hosts
                    found = DistSSHRun.resolve_remote_julia(host, "auto")
                    @test found isa AbstractString
                    found isa AbstractString || error("expected remote julia path")
                    @test isabspath(found) || startswith(found, '/')
                    @test found != "julia"
                    ver = DistSSHRun.get_remote_julia_version(host, found)
                    @test ver isa VersionNumber
                    @test ver.major == ctrl_ver.major
                    @test ver.minor == ctrl_ver.minor
                    _ssh_e2e_record_julia!(suite, "remote($(host))", found, string(ver))
                    _assert_ssh_e2e_api_ok(
                        suite,
                        "remote_julia_$(host)",
                        true,
                        "path=$(found) ver=$(ver)",
                    )
                end
            end
        end

        @testset "run_on_host exitcode" begin
            _e2e_announce("run_on_host exitcode")
            withenv(_e2e_base_env()...) do
                host = hosts[1]
                ok = DistSSHRun.run_on_host(host, ["--version"])
                @test ok.exitcode == 0
                fail = DistSSHRun.run_on_host(host, ["-e", "exit(3)"])
                @test fail.exitcode == 3
                _assert_ssh_e2e_api_ok(
                    suite,
                    "run_on_host_exitcode",
                    fail.exitcode == 3 && ok.exitcode == 0,
                    "ok=$(ok.exitcode) fail=$(fail.exitcode)",
                )
            end
        end

        # Remote suite (both docker workers). Local with_kit demos live in
        # test/integration/demos/with_kit.jl — not duplicated here.
        proj = suite.project_remote
        _stage_ssh_e2e_remote_host!(proj)
        smoke = joinpath(proj, "smoke.jl")
        echo_script = joinpath(proj, "demos", "with_kit", "square_echo.jl")
        pi_echo = joinpath(proj, "demos", "without_kit", "pi_echo.jl")
        pi_file = joinpath(proj, "demos", "without_kit", "pi_file.jl")

        @testset "setup --delete (clean slate)" begin
            _e2e_announce("setup --delete (clean slate)")
            proc, out = _run_kit_setup(;
                setup_args = ["--delete", "--remote-path", remote_root, setup_hosts...],
                project_root = proj,
                extra_env = _e2e_base_env(),
            )
            _assert_ssh_e2e_ok(suite, "setup_delete", proc, out; project = proj, kit = :setup)
            for host in hosts
                p, ssh_out = _ssh_e2e_ssh(host, "test ! -e $(remote_root)/Project.toml")
                _assert_proc_ok(p, ssh_out; label = "delete $(host) Project.toml gone")
            end
        end

        @testset "setup --rsync" begin
            _e2e_announce("setup --rsync")
            proc, out = _run_kit_setup(;
                setup_args = ["--rsync", "--remote-path", remote_root, setup_hosts...],
                project_root = proj,
                extra_env = _e2e_base_env(),
            )
            _assert_ssh_e2e_ok(suite, "setup_rsync", proc, out; project = proj, kit = :setup)
            for host in hosts
                p, ssh_out = _run_subprocess(
                    Cmd(
                        [
                            "ssh", "-F", g.ssh_config, host,
                            "test -f $(remote_root)/Project.toml",
                        ]
                    )
                )
                _assert_proc_ok(p, ssh_out; label = "rsync $(host) Project.toml")
            end
        end

        @testset "setup --instantiate" begin
            _e2e_announce("setup --instantiate")
            proc, out = _run_kit_setup(;
                setup_args = ["--instantiate", "--remote-path", remote_root, setup_hosts...],
                project_root = proj,
                extra_env = _e2e_base_env(),
            )
            _assert_ssh_e2e_ok(suite, "setup_instantiate", proc, out; project = proj, kit = :setup)
        end

        @testset "setup --check (major.minor; no --ignore-julia-version)" begin
            _e2e_announce("setup --check (major.minor; no --ignore-julia-version)")
            # rsync excludes .git/; --check warns on missing remote hash but must
            # still pass Julia major.minor + project/deps. Git parity is not
            # claimed for the rsync path (see docker-ssh README).
            proc, out = _run_kit_setup(;
                setup_args = ["--check", "--remote-path", remote_root, setup_hosts...],
                project_root = proj,
                extra_env = merge(_e2e_base_env(), Dict("DISTSSHKIT_QUIET" => "0")),
            )
            _assert_ssh_e2e_ok(suite, "setup_check", proc, out; project = proj, kit = :setup)
            @test occursin("Julia", out)
        end

        @testset "setup --juliaup (mismatch then align to kit parent)" begin
            _e2e_announce("setup --juliaup (mismatch then align to kit parent)")
            ch = _ssh_e2e_julia_channels()
            @test ch.alt != ch.default
            host = hosts[1]
            try
                _ssh_e2e_juliaup_default!(host, ch.alt)
                proc_bad, out_bad = _run_kit_setup(;
                    setup_args = ["--check", "--remote-path", remote_root, setup_hosts...],
                    project_root = proj,
                    extra_env = merge(_e2e_base_env(), Dict("DISTSSHKIT_QUIET" => "0")),
                )
                _ssh_e2e_record!(
                    suite, "setup_check_after_alt", proc_bad, out_bad;
                    expect_ok = false, project = proj, kit = :setup,
                )
                @test proc_bad.exitcode != 0
                @test occursin("mismatch", lowercase(out_bad)) ||
                    occursin("version", lowercase(out_bad))

                proc_up, out_up = _run_kit_setup(;
                    command = "up",
                    setup_args = [setup_hosts...],
                    project_root = proj,
                    extra_env = merge(_e2e_base_env(), Dict("DISTSSHKIT_QUIET" => "0")),
                )
                _assert_ssh_e2e_ok(suite, "setup_juliaup", proc_up, out_up; project = proj, kit = :setup)

                DistSSHRun.clear_detect_julia_path_cache!()
                withenv(_e2e_base_env()...) do
                    found = DistSSHRun.resolve_remote_julia(host, "auto")
                    @test found isa AbstractString
                    found isa AbstractString || error("expected remote julia after --juliaup")
                    ver = DistSSHRun.get_remote_julia_version(host, found)
                    @test ver isa VersionNumber
                    ver isa VersionNumber || error("expected remote version")
                    @test ver.major == VERSION.major
                    @test ver.minor == VERSION.minor
                    _assert_ssh_e2e_api_ok(
                        suite,
                        "juliaup_aligned_$(host)",
                        true,
                        "path=$(found) ver=$(ver) channel=$(ch.default)",
                    )
                end

                proc_ok, out_ok = _run_kit_setup(;
                    setup_args = ["--check", "--remote-path", remote_root, setup_hosts...],
                    project_root = proj,
                    extra_env = merge(_e2e_base_env(), Dict("DISTSSHKIT_QUIET" => "0")),
                )
                _assert_ssh_e2e_ok(
                    suite, "setup_check_after_juliaup", proc_ok, out_ok;
                    project = proj, kit = :setup,
                )
            finally
                for h in hosts
                    try
                        _ssh_e2e_juliaup_default!(h, ch.default)
                    catch e
                        @warn "restore juliaup default failed" host = h exception = e
                    end
                end
                DistSSHRun.clear_detect_julia_path_cache!()
            end
        end

        @testset "setup --juliaup parent + one remote" begin
            _e2e_announce("setup --juliaup parent + one remote")
            ch = _ssh_e2e_julia_channels()
            @test ch.alt != ch.default
            host = hosts[1]
            parent_restore = _ssh_e2e_local_juliaup_default_channel()
            try
                _ssh_e2e_juliaup_default_local!(ch.alt)
                _ssh_e2e_juliaup_default!(host, ch.alt)
                parent_alt = _ssh_e2e_local_juliaup_julia_version()
                alt_parts = split(ch.alt, '.')
                @test length(alt_parts) >= 2
                @test parent_alt.major == parse(Int, alt_parts[1])
                @test parent_alt.minor == parse(Int, alt_parts[2])
                @test DistSSHRun.julia_version_mismatch_kind(VERSION, parent_alt) == :minor

                proc_up, out_up = _run_kit_setup(;
                    command = "up",
                    setup_args = ["parent", DistSSHRun.setup_cli_host_token(host)],
                    project_root = proj,
                    extra_env = merge(_e2e_base_env(), Dict("DISTSSHKIT_QUIET" => "0")),
                )
                _assert_ssh_e2e_ok(
                    suite, "setup_juliaup_parent_remote", proc_up, out_up;
                    project = proj, kit = :setup,
                )
                @test occursin("parent", lowercase(out_up))

                parent_ver = _ssh_e2e_local_juliaup_julia_version()
                @test parent_ver.major == VERSION.major
                @test parent_ver.minor == VERSION.minor
                _assert_ssh_e2e_api_ok(
                    suite,
                    "juliaup_aligned_parent",
                    true,
                    "ver=$(parent_ver) channel=$(ch.default)",
                )

                DistSSHRun.clear_detect_julia_path_cache!()
                withenv(_e2e_base_env()...) do
                    found = DistSSHRun.resolve_remote_julia(host, "auto")
                    @test found isa AbstractString
                    found isa AbstractString || error("expected remote julia after --juliaup")
                    ver = DistSSHRun.get_remote_julia_version(host, found)
                    @test ver isa VersionNumber
                    ver isa VersionNumber || error("expected remote version")
                    @test ver.major == VERSION.major
                    @test ver.minor == VERSION.minor
                    _assert_ssh_e2e_api_ok(
                        suite,
                        "juliaup_aligned_$(host)_with_parent",
                        true,
                        "path=$(found) ver=$(ver) channel=$(ch.default)",
                    )
                end

                # Second remote was not a target; leave it alone. Re-check listed hosts.
                proc_ok, out_ok = _run_kit_setup(;
                    setup_args = ["--check", "--remote-path", remote_root, DistSSHRun.setup_cli_host_token(host)],
                    project_root = proj,
                    extra_env = merge(_e2e_base_env(), Dict("DISTSSHKIT_QUIET" => "0")),
                )
                _assert_ssh_e2e_ok(
                    suite, "setup_check_after_juliaup_parent", proc_ok, out_ok;
                    project = proj, kit = :setup,
                )
            finally
                try
                    _ssh_e2e_juliaup_default_local!(parent_restore)
                catch e
                    @warn "restore kit parent juliaup default failed" exception = e
                end
                try
                    _ssh_e2e_juliaup_default!(host, ch.default)
                catch e
                    @warn "restore juliaup default failed" host = host exception = e
                end
                DistSSHRun.clear_detect_julia_path_cache!()
            end
        end

        @testset "setup --juliaup-update (default channel unchanged)" begin
            _e2e_announce("setup --juliaup-update (default channel unchanged)")
            host = hosts[1]
            before = _ssh_e2e_juliaup_remote_default_channel(host)
            proc, out = _run_kit_setup(;
                command = "up",
                setup_args = ["update", DistSSHRun.setup_cli_host_token(host)],
                project_root = proj,
                extra_env = merge(_e2e_base_env(), Dict("DISTSSHKIT_QUIET" => "0")),
            )
            _assert_ssh_e2e_ok(
                suite, "setup_juliaup_update", proc, out;
                project = proj, kit = :setup,
            )
            after = _ssh_e2e_juliaup_remote_default_channel(host)
            @test after == before
            _assert_ssh_e2e_api_ok(
                suite,
                "juliaup_update_default_unchanged_$(host)",
                after == before,
                "channel=$(after)",
            )
        end

        @testset "setup --runtest (job Pkg.test)" begin
            _e2e_announce("setup --runtest (job Pkg.test)")
            proc, out = _run_kit_setup(;
                setup_args = ["--runtest", "--remote-path", remote_root, setup_hosts...],
                project_root = proj,
                extra_env = _e2e_base_env(),
            )
            _assert_ssh_e2e_ok(suite, "setup_runtest", proc, out; project = proj, kit = :setup)
        end

        @testset "setup --runtest fails when job tests fail" begin
            _e2e_announce("setup --runtest fails when job tests fail")
            try
                _ssh_e2e_push_job_runtests!(hosts, remote_root; fail = true)
                proc, out = _run_kit_setup(;
                    setup_args = ["--runtest", "--remote-path", remote_root, setup_hosts...],
                    project_root = proj,
                    extra_env = merge(_e2e_base_env(), Dict("DISTSSHKIT_QUIET" => "0")),
                )
                _ssh_e2e_record!(
                    suite, "setup_runtest_fail", proc, out;
                    expect_ok = false, project = proj, kit = :setup,
                )
                @test proc.exitcode != 0
            finally
                _ssh_e2e_push_job_runtests!(hosts, remote_root; fail = false)
            end
        end

        @testset "size two remotes" begin
            _e2e_announce("size two remotes")
            proc, out = _run_kit_size(;
                size_args = ["-q", remote_tokens...],
                project_root = proj,
                extra_env = merge(_e2e_base_env(), Dict("DISTSSHKIT_QUIET" => "0")),
            )
            _assert_ssh_e2e_ok(suite, "size_remotes", proc, out)
            @test occursin(hosts[1], out)
            @test occursin(hosts[2], out)
            @test occursin(" GB", out)
            @test occursin("Total:", out)
        end

        @testset "drive square_echo two remotes" begin
            _e2e_announce("drive square_echo two remotes")
            proc, out = _run_kit_drive(;
                script = echo_script,
                host_root = proj,
                parent_workers = 0,
                child_hosts = remote_tokens,
                script_args = ["--n", "4"],
                drive_flags = ["-y", "-q"],
                extra_env = _e2e_base_env(),
            )
            _assert_ssh_e2e_ok(suite, "drive_square_echo", proc, out)
            @test occursin("param^2:", out)
            _assert_kit_progress_done(joinpath(proj, "demos", "with_kit", "output"); kind = :drive)
        end

        @testset "drive square_file CSV is local (kit parent main)" begin
            _e2e_announce("drive square_file CSV is local (kit parent main)")
            square_file = joinpath(proj, "demos", "with_kit", "square_file.jl")
            out_csv = joinpath(proj, "demos", "with_kit", "output", "square_results.csv")
            isfile(out_csv) && rm(out_csv)
            proc, out = _run_kit_drive(;
                script = square_file,
                host_root = proj,
                parent_workers = 0,
                child_hosts = remote_tokens,
                script_args = ["--n", "4"],
                drive_flags = ["-y", "-q"],
                extra_env = _e2e_base_env(),
            )
            _assert_ssh_e2e_ok(suite, "drive_square_file", proc, out)
            @test isfile(out_csv)
            @test occursin("param,result", read(out_csv, String))
            p, _ = _ssh_e2e_ssh(hosts[1], "test ! -e $(remote_root)/demos/with_kit/output/square_results.csv")
            _assert_proc_ok(p, ""; label = "square_file CSV absent on remote")
            _assert_kit_progress_done(dirname(out_csv); kind = :drive)
        end

        @testset "drive collect bytes written on workers" begin
            _e2e_announce("drive collect bytes written on workers")
            script = joinpath(proj, "worker_file.jl")
            collect_root = joinpath(proj, "output")
            isdir(collect_root) && rm(collect_root; recursive = true)

            proc, out = _run_kit_drive(;
                script = script,
                host_root = proj,
                parent_workers = 0,
                child_hosts = remote_tokens,
                drive_flags = ["-y", "-q"],
                extra_env = _e2e_base_env(),
            )
            _assert_ssh_e2e_ok(suite, "drive_worker_file", proc, out)
            _assert_kit_progress_done(collect_root; kind = :drive)

            worker_jl = """
            d = $(repr(joinpath(remote_root, "output")))
            io = IOBuffer()
            if isdir(d)
                for (root, _, files) in walkdir(d)
                    for name in files
                        startswith(name, "worker_") && endswith(name, ".txt") || continue
                        write(io, read(joinpath(root, name)))
                    end
                end
            end
            print(String(take!(io)))
            """
            p, remote_body = withenv(_e2e_base_env()...) do
                _e2e_run_on_host(hosts[1], ["-e", worker_jl])
            end
            _assert_proc_ok(p, remote_body; label = "run_on_host cat worker files")
            @test occursin("DISTSSHKIT_E2E_WORKER_FILE", remote_body)

            local_files = filter(
                f -> startswith(basename(f), "worker_"),
                isdir(collect_root) ? readdir(collect_root; join = true) : String[]
            )
            @test !isempty(local_files)
            @test any(f -> occursin("DISTSSHKIT_E2E_WORKER_FILE", read(f, String)), local_files)
            @test Set(h.host for h in DistSSHRun.kit_result_from_dir(collect_root).hosts) == Set(hosts)

            for f in local_files
                rm(f)
            end
            proc, out = _run_kit_drive_collect(;
                collect_root = collect_root,
                hosts = hosts,
                host_root = proj,
                extra_env = _e2e_base_env(),
            )
            _assert_ssh_e2e_ok(suite, "collect_missing", proc, out)
            local_files = filter(
                f -> startswith(basename(f), "worker_"),
                readdir(collect_root; join = true)
            )
            @test !isempty(local_files)
            let sample = local_files[1]
                @test occursin("DISTSSHKIT_E2E_WORKER_FILE", read(sample, String))

                write(sample, "LOCALJUNK\n")
                proc, out = _run_kit_drive_collect(;
                    collect_root = collect_root,
                    hosts = hosts,
                    host_root = proj,
                    extra_env = _e2e_base_env(),
                )
                _assert_ssh_e2e_ok(suite, "collect_missing_skip", proc, out)
                @test read(sample, String) == "LOCALJUNK\n"

                proc, out = _run_kit_drive_collect(;
                    collect_root = collect_root,
                    hosts = hosts,
                    overwrite = true,
                    host_root = proj,
                    extra_env = _e2e_base_env(),
                )
                _assert_ssh_e2e_ok(suite, "collect_overwrite", proc, out)
                @test occursin("DISTSSHKIT_E2E_WORKER_FILE", read(sample, String))
                @test !occursin("LOCALJUNK", read(sample, String))
            end
        end

        @testset "drive remote worker error is a real failure" begin
            _e2e_announce("drive remote worker error is a real failure")
            proc, out = _run_kit_drive(;
                script = joinpath(proj, "fail.jl"),
                host_root = proj,
                parent_workers = 0,
                child_hosts = remote_tokens,
                drive_flags = ["-y", "-q"],
                extra_env = merge(_e2e_base_env(), Dict("DISTSSHKIT_QUIET" => "0")),
            )
            _ssh_e2e_record!(
                suite, "drive_remote_error", proc, out;
                expect_ok = false, project = proj,
            )
            @test proc.exitcode != 0
            @test occursin("DISTSSHKIT_E2E_REMOTE_FAIL", out)
        end

        @testset "drive mixed local+remotes smoke" begin
            _e2e_announce("drive mixed local+remotes smoke")
            proc, out = _run_kit_drive(;
                script = smoke,
                host_root = proj,
                parent_workers = 1,
                child_hosts = remote_tokens,
                drive_flags = ["-y", "-q"],
                extra_env = _e2e_base_env(),
            )
            _assert_ssh_e2e_ok(suite, "drive_mixed", proc, out)
            @test occursin("DISTSSHKIT_RUNNER_SMOKE_OK nw=3", out)
        end

        @testset "go pi_echo both remotes" begin
            _e2e_announce("go pi_echo both remotes")
            proc, out = _run_kit_go(;
                script = pi_echo,
                hosts = remote_tokens,
                script_args = ["--n", "32"],
                project_root = proj,
                go_flags = ["-y"],
                extra_env = merge(_e2e_base_env(), Dict("DISTSSHKIT_QUIET" => "0")),
            )
            _assert_ssh_e2e_ok(suite, "go_pi_echo", proc, out; project = proj, kit = :go)
            @test occursin(hosts[1], out)
            @test occursin(hosts[2], out)
            @test count(r"π ≈", out) >= 2
            go_echo_batch = _ssh_e2e_latest_go_batch(proj)
            @test go_echo_batch !== nothing
            go_echo_batch === nothing && error("expected go batch for pi_echo")
            _assert_kit_progress_done(go_echo_batch; kind = :go)
        end

        @testset "go pi_file both remotes + collect" begin
            _e2e_announce("go pi_file both remotes + collect")
            # Queue always sets job_id. The mark is `-L` on the remote, not `--eval`.
            job_id = "e2e-go-1"
            proc, out = _run_kit_go(;
                script = pi_file,
                hosts = remote_tokens,
                script_args = ["--n", "32"],
                project_root = proj,
                go_flags = ["-y"],
                extra_env = merge(
                    _e2e_base_env(),
                    Dict(
                        "DISTSSHKIT_QUIET" => "0",
                        "DISTSSHKIT_JOB_ID" => job_id,
                    ),
                ),
            )
            _assert_ssh_e2e_ok(suite, "go_pi_file", proc, out; project = proj, kit = :go)
            batch = _ssh_e2e_latest_go_batch(proj)
            @test batch !== nothing
            batch === nothing && error("expected go batch for pi_file")
            _assert_kit_progress_done(batch; kind = :go)
            mark = DistSSHRun.kit_job_pkill_pattern(job_id)
            for host in hosts
                slot = joinpath(batch, host)
                results = joinpath(slot, "pi_results.txt")
                mark_path = joinpath(slot, mark)
                @test isdir(slot)
                @test isfile(results)
                isfile(results) && @test occursin("pi=", read(results, String))
                @test isfile(mark_path)
                isfile(mark_path) && @test strip(read(mark_path, String)) ==
                    DistSSHRun.kit_job_mark_comment(job_id)
            end
        end

        @testset "go pi_file with --output-dir" begin
            _e2e_announce("go pi_file with --output-dir")
            custom = joinpath(proj, "go_cli_output")
            isdir(custom) && rm(custom; recursive = true)
            proc, out = _run_kit_go(;
                script = pi_file,
                hosts = remote_tokens,
                script_args = ["--n", "32"],
                project_root = proj,
                go_flags = ["-y", "--output-dir", custom],
                extra_env = merge(_e2e_base_env(), Dict("DISTSSHKIT_QUIET" => "0")),
            )
            _assert_ssh_e2e_ok(suite, "go_pi_file_output_dir", proc, out; project = proj, kit = :go)
            for host in hosts
                slot = joinpath(custom, host)
                @test isdir(slot)
                @test isfile(joinpath(slot, "pi_results.txt"))
                @test occursin("pi=", read(joinpath(slot, "pi_results.txt"), String))
            end
        end

        # Own remote root: do not reuse `remote_root` (later nonempty `--rsync` must still refuse).
        @testset "go --rsync empty remote" begin
            _e2e_announce("go --rsync empty remote")
            oneshot = _ssh_e2e_go_rsync_remote_root()
            oneshot_env = _ssh_e2e_env(; remote_project = oneshot)
            try
                proc, out = _run_kit_setup(;
                    setup_args = ["--delete", "--remote-path", oneshot, setup_hosts...],
                    project_root = proj,
                    extra_env = oneshot_env,
                )
                _assert_ssh_e2e_ok(suite, "go_rsync_empty_delete", proc, out; project = proj, kit = :setup)
                for host in hosts
                    p, ssh_out = _ssh_e2e_ssh(host, "test ! -e $(oneshot)/Project.toml")
                    _assert_proc_ok(p, ssh_out; label = "go --rsync empty $(host) gone")
                end

                proc, out = _run_kit_go(;
                    script = pi_echo,
                    hosts = remote_tokens,
                    script_args = ["--n", "32"],
                    project_root = proj,
                    go_flags = ["-y", "--rsync"],
                    extra_env = merge(oneshot_env, Dict("DISTSSHKIT_QUIET" => "0")),
                )
                _assert_ssh_e2e_ok(suite, "go_rsync_empty", proc, out; project = proj, kit = :go)
                @test occursin(hosts[1], out)
                @test occursin(hosts[2], out)
                @test count(r"π ≈", out) >= 2
                for host in hosts
                    p, ssh_out = _run_subprocess(
                        Cmd(
                            [
                                "ssh", "-F", g.ssh_config, host,
                                "test -f $(oneshot)/Project.toml",
                            ]
                        )
                    )
                    _assert_proc_ok(p, ssh_out; label = "go --rsync $(host) Project.toml")
                end
            finally
                proc, out = _run_kit_setup(;
                    setup_args = ["--delete", "--remote-path", oneshot, setup_hosts...],
                    project_root = proj,
                    extra_env = oneshot_env,
                )
                _assert_ssh_e2e_ok(
                    suite, "go_rsync_empty_cleanup", proc, out;
                    project = proj, kit = :setup,
                )
            end
        end

        # `~/…` remote root: setup + drive still run. square_file CSV is local.
        @testset "drive square_file collect with tilde remote root" begin
            _e2e_announce("drive square_file collect with tilde remote root")
            tilde_root = _ssh_e2e_tilde_remote_root()
            tilde_env = _ssh_e2e_env(; remote_project = tilde_root)
            square_file = joinpath(proj, "demos", "with_kit", "square_file.jl")
            out_csv = joinpath(proj, "demos", "with_kit", "output", "square_results.csv")
            isfile(out_csv) && rm(out_csv)

            proc, out = _run_kit_setup(;
                setup_args = ["--delete", "--remote-path", tilde_root, setup_hosts...],
                project_root = proj,
                extra_env = tilde_env,
            )
            _assert_ssh_e2e_ok(suite, "tilde_delete", proc, out; project = proj, kit = :setup)

            proc, out = _run_kit_setup(;
                setup_args = ["--rsync", "--remote-path", tilde_root, setup_hosts...],
                project_root = proj,
                extra_env = tilde_env,
            )
            _assert_ssh_e2e_ok(suite, "tilde_rsync", proc, out; project = proj, kit = :setup)

            proc, out = _run_kit_setup(;
                setup_args = ["--instantiate", "--remote-path", tilde_root, setup_hosts...],
                project_root = proj,
                extra_env = tilde_env,
            )
            _assert_ssh_e2e_ok(suite, "tilde_instantiate", proc, out; project = proj, kit = :setup)

            proc, out = _run_kit_drive(;
                script = square_file,
                host_root = proj,
                parent_workers = 0,
                child_hosts = remote_tokens,
                script_args = ["--n", "4"],
                drive_flags = ["-y", "-q"],
                extra_env = tilde_env,
            )
            _assert_ssh_e2e_ok(suite, "tilde_drive_square_file", proc, out)
            @test isfile(out_csv)
            @test occursin("param,result", read(out_csv, String))
        end

        # Julian API path (same remotes): setup! → go! / pipeline!
        @testset "Julian API setup! + go!/pipeline!" begin
            _e2e_announce("Julian API setup! + go!/pipeline!")
            withenv(_e2e_base_env()...) do
                session = KitSession(
                    project = proj,
                    workers = remote_tokens,
                    remote = remote_root,
                    yes = true,
                    quiet = true,
                )
                prep = setup!(session, :delete, :rsync, :instantiate)
                _assert_ssh_e2e_api_ok(
                    suite,
                    "api_setup",
                    prep.ok,
                    "hosts=$(length(prep.hosts))",
                )
                @test prep.ok
                @test !prep.cancelled
                @test length(prep.hosts) == length(hosts)
                @test all(h -> h.ok, prep.hosts)

                go_out = joinpath(proj, "go_api_output")
                isdir(go_out) && rm(go_out; recursive = true)
                go_res = go!(
                    pi_file,
                    remote_tokens[1];
                    project = proj,
                    remote = remote_root,
                    args = ["--n", "16"],
                    yes = true,
                    quiet = true,
                    julia = "auto",
                    output_dir = go_out,
                )
                _assert_ssh_e2e_api_ok(suite, "api_go", go_res.ok)
                @test go_res.ok
                @test go_res.output_dir == DistSSHRun.canonical_local_path(go_out)
                go_txt = joinpath(go_res.output_dir, hosts[1], "pi_results.txt")
                @test isfile(go_txt)
                @test occursin("pi=", read(go_txt, String))

                worker_script = joinpath(proj, "worker_file.jl")
                worker_out = joinpath(proj, "output")
                isdir(worker_out) && rm(worker_out; recursive = true)
                pipe_res = pipeline!(
                    worker_script,
                    remote_tokens[2];
                    project = proj,
                    remote = remote_root,
                    collect = true,
                    enable_log = false,
                    yes = true,
                    quiet = true,
                    julia = "auto",
                )
                _assert_ssh_e2e_api_ok(suite, "api_pipeline", pipe_res.ok)
                @test pipe_res.ok
                worker_txts = filter(
                    f -> startswith(basename(f), "worker_"),
                    isdir(worker_out) ? readdir(worker_out; join = true) : String[],
                )
                @test !isempty(worker_txts)
                @test any(f -> occursin("DISTSSHKIT_E2E_WORKER_FILE", read(f, String)), worker_txts)
            end
        end

        # #144: in-process `drive!` tears down its remote workers per call
        # (not only at Julia exit). Two calls in this one process must each
        # see `nw=2` — a leak from the first would surface as `nw=4` in the
        # second. `pipeline!` above already did one in-process remote drive;
        # these are the reentrant calls that guard the regression over SSH.
        @testset "in-process drive! is reentrant (no worker leak)" begin
            _e2e_announce("in-process drive! is reentrant (no worker leak)")
            withenv(_e2e_base_env()...) do
                for call in 1:2
                    out = mktemp() do out_path, out_io
                        res = redirect_stdout(out_io) do
                            drive!(
                                smoke,
                                remote_tokens;
                                project = proj,
                                remote = remote_root,
                                julia = "auto",
                                verbosity = :verbose,
                                yes = true,
                            )
                        end
                        flush(out_io)
                        @test res.ok
                        @test res.exit_code == 0
                        @test Set(h.host for h in res.hosts) == Set(hosts)
                        @test all(h -> h.ok, res.hosts)
                        read(out_path, String)
                    end
                    _assert_ssh_e2e_api_ok(
                        suite,
                        "drive_reentrant_call$(call)",
                        occursin("DISTSSHKIT_RUNNER_SMOKE_OK nw=2", out),
                        "call=$(call)",
                    )
                    @test occursin("DISTSSHKIT_RUNNER_SMOKE_OK nw=2", out)
                    @test nworkers() == 1
                end
            end
        end

        # #148: SIGKILL the detached master after heartbeat is *running*
        # (`--worker` appears at addprocs, monitors start much later). atexit
        # cannot help. The ssh child may keep the tunnel, so reap is the
        # shortened deadline. Silent-stall scheduling is unit-tested.
        @testset "detached drive kill reaps remote workers" begin
            _e2e_announce("detached drive kill reaps remote workers")
            sleep_script = joinpath(proj, "sleep.jl")
            host = hosts[1]
            log_dir = joinpath(suite.logs, "heartbeat-drive")
            mkpath(log_dir)
            # Detached drive skips global pkill; drop leftovers so pgrep is ours.
            _ssh_e2e_ssh(host, "pkill -f 'julia.*--worker' || true")
            hb_env = merge(
                _e2e_base_env(), Dict(
                    "DISTRIBUTED_HEARTBEAT_INTERVAL_SEC" => "1",
                    "DISTRIBUTED_HEARTBEAT_DEADLINE_SEC" => "5",
                    "DISTRIBUTED_INIT_DELAY_SEC" => "0",
                )
            )
            kp = withenv(hb_env...) do
                DistSSHRun.execute!(
                    :drive,
                    sleep_script,
                    ["child:$(host):1"];
                    project = proj,
                    remote = remote_root,
                    detached = true,
                    yes = true,
                    quiet = true,
                    enable_log = true,
                    log_dir = log_dir,
                )
            end
            function _hb_log_text()
                isdir(log_dir) || return ""
                buf = IOBuffer()
                for f in readdir(log_dir; join = true)
                    endswith(f, ".log") || continue
                    print(buf, read(f, String))
                end
                return String(take!(buf))
            end
            function _worker_pids()
                _, body = _ssh_e2e_ssh(host, "pgrep -f 'julia.*--worker' || true")
                return Int[parse(Int, s) for s in split(strip(body); keepempty = false)]
            end
            # A queue that lost its in-memory `KitProcess` (e.g. restarted
            # mid-job) falls back to `kit.pid` — assert it names this run's
            # real OS pid before using it below instead of `kp.process`.
            pid_path = joinpath(log_dir, "kit.pid")
            pid_ready = false
            t0p = time()
            while (time() - t0p) < 10
                isfile(pid_path) && (pid_ready = true; break)
                sleep(0.1)
            end
            _assert_ssh_e2e_api_ok(suite, "kit_pid_file", pid_ready, "path=$(pid_path)")
            @test pid_ready
            rec = pid_ready ? DistSSHRun._read_kit_pid_record(log_dir) : nothing
            detached_pid = rec === nothing ? -1 : rec.pid
            @test detached_pid == getpid(kp.process)

            ready = false
            t0 = time()
            while (time() - t0) < 90
                # Prefix is logged before `@everywhere`; the ✓ is after monitors start.
                if occursin("Starting heartbeat monitors... ✓", _hb_log_text())
                    ready = true
                    break
                end
                process_running(kp.process) || break
                sleep(0.5)
            end
            pids = _worker_pids()
            _assert_ssh_e2e_api_ok(
                suite,
                "heartbeat_ready",
                ready && !isempty(pids),
                "ready=$(ready) pids=$(pids) log=$(repr(_hb_log_text()[max(1, end - 400):end]))",
            )
            @test ready
            @test !isempty(pids)
            # Kill by the `kit.pid`-derived pid, not `kp.process` — the path a
            # queue without the original `KitProcess` handle would take.
            run(`kill -9 $(detached_pid)`)
            wait(kp.process)
            gone = false
            t1 = time()
            leftover = pids
            while (time() - t1) < 30
                live = Set(_worker_pids())
                leftover = Int[p for p in pids if p in live]
                if isempty(leftover)
                    gone = true
                    break
                end
                sleep(0.5)
            end
            _assert_ssh_e2e_api_ok(suite, "heartbeat_reap", gone, "leftover=$(leftover)")
            @test gone
        end

        # Detached SSH ride writes `kit.hosts`; `terminate!` SIGTERMs then
        # tagged `pkill` (never `julia.*--worker`). Parent-only ride is unit.
        @testset "detached ride terminate! reaps remote workers" begin
            _e2e_announce("detached ride terminate! reaps remote workers")
            ride_script = joinpath(proj, "ride_sleep.jl")
            host = hosts[1]
            _ssh_e2e_ssh(host, "pkill -f 'julia.*--worker' || true")
            kp = withenv(_e2e_base_env()...) do
                DistSSHRun.execute!(
                    :ride,
                    ride_script,
                    ["child:$(host):1"];
                    project = proj,
                    remote = remote_root,
                    detached = true,
                    yes = true,
                    quiet = true,
                    spi_check = false,
                    job_id = "e2e-ride-term",
                )
            end
            out = kp.output_dir
            @test out isa AbstractString
            hosts_path = joinpath(String(out), "kit.hosts")
            hosts_ready = false
            t0h = time()
            while (time() - t0h) < 90
                if isfile(hosts_path) && !isempty(strip(read(hosts_path, String)))
                    hosts_ready = true
                    break
                end
                process_running(kp.process) || break
                sleep(0.5)
            end
            listed = hosts_ready ? DistSSHRun._read_kit_hosts(String(out)) : String[]
            function _kit_snip(name)
                p = joinpath(String(out), name)
                isfile(p) || return ""
                text = strip(read(p, String))
                return last(text, min(length(text), 800))
            end
            err_snip = _kit_snip("kit.out")
            err_err = _kit_snip("kit.err")
            _assert_ssh_e2e_api_ok(
                suite,
                "ride_kit_hosts",
                hosts_ready && host in listed,
                "ready=$(hosts_ready) listed=$(listed) path=$(hosts_path) out=$(repr(err_snip)) err=$(repr(err_err))",
            )
            @test hosts_ready
            @test host in listed

            function _ride_worker_pids()
                _, body = _ssh_e2e_ssh(host, "pgrep -f 'julia.*--worker' || true")
                return Int[parse(Int, s) for s in split(strip(body); keepempty = false)]
            end
            workers_ready = false
            t0w = time()
            while (time() - t0w) < 60
                if !isempty(_ride_worker_pids())
                    workers_ready = true
                    break
                end
                process_running(kp.process) || break
                sleep(0.5)
            end
            pids = _ride_worker_pids()
            _assert_ssh_e2e_api_ok(
                suite,
                "ride_workers_up",
                workers_ready && !isempty(pids),
                "pids=$(pids)",
            )
            @test workers_ready
            @test !isempty(pids)

            result = DistSSHRun.terminate!(kp; grace = 15)
            @test result isa DistSSHRun.KitRunResult
            @test result.kind === :ride
            @test !process_running(kp.process)

            gone = false
            leftover = pids
            t1 = time()
            while (time() - t1) < 30
                live = Set(_ride_worker_pids())
                leftover = Int[p for p in pids if p in live]
                if isempty(leftover)
                    gone = true
                    break
                end
                sleep(0.5)
            end
            _assert_ssh_e2e_api_ok(suite, "ride_terminate_reap", gone, "leftover=$(leftover)")
            @test gone
        end

        @testset "setup --rsync refuses nonempty" begin
            _e2e_announce("setup --rsync refuses nonempty")
            proc, out = _run_kit_setup(;
                setup_args = ["--rsync", "--remote-path", remote_root, setup_hosts[1]],
                project_root = proj,
                extra_env = merge(_e2e_base_env(), Dict("DISTSSHKIT_QUIET" => "0")),
            )
            _ssh_e2e_record!(suite, "setup_rsync_refuse", proc, out; expect_ok = false, project = proj, kit = :setup)
            @test proc.exitcode != 0
            @test occursin("refusing", lowercase(out))
        end

        @testset "inter-child SSH (child-1 → child-2)" begin
            _e2e_announce("inter-child SSH (child-1 → child-2)")
            cmd = Cmd(
                [
                    "ssh", "-F", g.ssh_config, hosts[1],
                    "ssh -o BatchMode=yes -o StrictHostKeyChecking=accept-new -o ConnectTimeout=5 " *
                        "dev@child-2 'echo inter-ok'",
                ]
            )
            proc, out = _run_subprocess(cmd)
            _assert_ssh_e2e_ok(suite, "inter_child_ssh", proc, out)
            @test occursin("inter-ok", out)
        end

        # Git path (separate remote root): bare on child-1 → clone → check hash → sync → --require-git.
        @testset "git clone + sync + require-git" begin
            _e2e_announce("git clone + sync + require-git")
            git_root = _ssh_e2e_git_remote_root()
            git_env = _ssh_e2e_env(; remote_project = git_root)
            seed = withenv(git_env...) do
                _ssh_e2e_seed_git_origin!(proj)
            end
            @test seed !== nothing
            seed === nothing && error("expected git origin seed")

            proc, out = _run_kit_setup(;
                setup_args = [
                    "--delete", "--remote-path", git_root, setup_hosts...,
                ],
                project_root = proj,
                extra_env = git_env,
            )
            _assert_ssh_e2e_ok(suite, "git_delete", proc, out; project = proj, kit = :setup)

            proc, out = _run_kit_setup(;
                setup_args = [
                    "--clone",
                    "--repo", seed.origin_workers,
                    "--remote-path", git_root,
                    setup_hosts...,
                ],
                project_root = proj,
                extra_env = git_env,
            )
            _assert_ssh_e2e_ok(suite, "git_clone", proc, out; project = proj, kit = :setup)

            proc, out = _run_kit_setup(;
                setup_args = [
                    "--instantiate", "--remote-path", git_root, setup_hosts...,
                ],
                project_root = proj,
                extra_env = git_env,
            )
            _assert_ssh_e2e_ok(suite, "git_instantiate", proc, out; project = proj, kit = :setup)

            proc, out = _run_kit_setup(;
                setup_args = ["--check", "--remote-path", git_root, setup_hosts...],
                project_root = proj,
                extra_env = merge(git_env, Dict("DISTSSHKIT_QUIET" => "0")),
            )
            _assert_ssh_e2e_ok(suite, "git_check", proc, out; project = proj, kit = :setup)
            local_before = DistSSHRun.get_local_git_hash(proj; short = 12)
            @test local_before isa String
            local_before isa String || error("expected local git hash")
            @test occursin(local_before, out)

            bumped = withenv(git_env...) do
                _ssh_e2e_git_bump_commit!(proj)
            end
            @test bumped isa String
            @test bumped != local_before

            proc, out = _run_kit_drive(;
                script = joinpath(proj, "smoke.jl"),
                host_root = proj,
                parent_workers = 0,
                child_hosts = remote_tokens,
                drive_flags = ["-y", "-q", "--require-git"],
                extra_env = merge(git_env, Dict("DISTSSHKIT_QUIET" => "0")),
            )
            _ssh_e2e_record!(
                suite, "git_require_git_mismatch", proc, out;
                expect_ok = false, project = proj,
            )
            @test proc.exitcode != 0
            @test occursin("mismatch", lowercase(out)) || occursin("could not be verified", lowercase(out))

            proc, out = _run_kit_setup(;
                setup_args = ["--sync", "--remote-path", git_root, setup_hosts...],
                project_root = proj,
                extra_env = merge(git_env, Dict("DISTSSHKIT_QUIET" => "0")),
            )
            _assert_ssh_e2e_ok(suite, "git_sync", proc, out; project = proj, kit = :setup)

            proc, out = _run_kit_setup(;
                setup_args = ["--check", "--remote-path", git_root, setup_hosts...],
                project_root = proj,
                extra_env = merge(git_env, Dict("DISTSSHKIT_QUIET" => "0")),
            )
            _assert_ssh_e2e_ok(suite, "git_check_after_sync", proc, out; project = proj, kit = :setup)
            bumped isa String || error("expected bumped git hash")
            @test occursin(bumped, out)

            # drive --require-git must pass when remotes have matching .git/
            proc, out = _run_kit_drive(;
                script = joinpath(proj, "smoke.jl"),
                host_root = proj,
                parent_workers = 0,
                child_hosts = remote_tokens,
                drive_flags = ["-y", "-q", "--require-git"],
                extra_env = git_env,
            )
            _assert_ssh_e2e_ok(suite, "git_drive_require_git", proc, out)
            @test occursin("DISTSSHKIT_RUNNER_SMOKE_OK", out)

            pulled = withenv(git_env...) do
                p = _ssh_e2e_git_bump_commit!(proj; message = "e2e-pull")
                _ssh_e2e_git_push!(proj)
                p
            end
            @test pulled isa String
            marker = read(joinpath(proj, "e2e_sync_marker.txt"), String)

            proc, out = _run_kit_setup(;
                setup_args = ["--pull", "--remote-path", git_root, setup_hosts...],
                project_root = proj,
                extra_env = merge(git_env, Dict("DISTSSHKIT_QUIET" => "0")),
            )
            _assert_ssh_e2e_ok(suite, "git_pull", proc, out; project = proj, kit = :setup)
            for host in hosts
                p, body = withenv(git_env...) do
                    _e2e_run_on_host(
                        host,
                        ["-e", "print(read($(repr(joinpath(git_root, "e2e_sync_marker.txt"))), String))"],
                    )
                end
                _assert_proc_ok(p, body; label = "pull marker $(host)")
                @test body == marker
            end
        end
    end
end
